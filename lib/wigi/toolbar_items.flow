import binarytree;
import wigi/toolbar_types;

export {
	// make WigiMenu based on WigiToolbarStyle
	makeWigiMenu(toolbarStyle : [WigiToolbarStyle]) -> WigiMenu;
	// check if section exists
	containInMenu(menu: WigiMenu, section: WigiMenuSection) -> bool;
	// check if subItem exists inside section
	containInSection(menu: WigiMenu, section: WigiMenuSection, subItem: WMSubItem) -> bool;

	WigiMenu : (
		items : Tree<WigiMenuSection, [WMSubItem]>
	);

	WigiMenuSection ::= WMUndoRedo, WMFontFamily, WMFontSize, WMAlign, WMFontOptions, WMParagraph, WMTools, WMAdditional;
		WMUndoRedo(); //Used for undo redo blocks
		WMFontFamily(); // Used for "Font Family" block
		WMFontSize(); //Used for "Font size" block
		WMAlign(); // Used for Aligning block
		WMFontOptions(); // Used for "Font options" block
		WMParagraph(); // Used for "Paragraph" block
		WMTools(); // Used for "+" (external blocks) block
		WMAdditional(); // Used or additional block

	WMSubItem ::= WMAddMedia, WMPageBreak, WMPageName, WMCoachSpeak, WMActiveArt, WMSmartArt, WMShortInteractives, WMFullInteractives, WMInsertIcon, WMTable, WMInteractiveTable,
					WMSpreadsheet, WMCalculatedCell, WMTemplate, WMInsertCSV, WMInsertDiagram, WMNaturalMath, WMSymbol, WMAddHyperlink, WMNativeWiki,
					WMVariableInitialization, WMAnnotatePalette, WMShowMeStructures, WMPastePlainText, WMAltText, WMHTMLResource, WMSpellCheck, WMProof,
					WMFontSizeControls, WMFontColorControls, WMUnderlineControls, WMScriptControls, WMBasicFonts, WMExtendedFonts;
		WMAddMedia();
		WMPageBreak();
		WMPageName();
		WMCoachSpeak();
		WMActiveArt();
		WMSmartArt();
		WMShortInteractives();
		WMFullInteractives();
		WMInsertIcon();
		WMTable();
		WMInteractiveTable();
		WMSpreadsheet();
		WMCalculatedCell();
		WMTemplate();
		WMInsertCSV();
		WMInsertDiagram();
		WMNaturalMath();
		WMSymbol();
		WMAddHyperlink();
		WMNativeWiki();
		WMVariableInitialization();
		WMAnnotatePalette();
		WMShowMeStructures();
		WMPastePlainText();
		WMAltText();
		WMHTMLResource();
		WMSpellCheck();
		WMProof();

		WMFontSizeControls();
		WMFontColorControls();
		WMUnderlineControls();
		WMScriptControls();

		WMBasicFonts();
		WMExtendedFonts();
}

addMenuSection(section, subItems) {
	\menu -> setTree(menu, section, subItems)
}

removeMenuSection(section) {
	\menu -> removeFromTree(menu, section)
}

processMenuSubItems(section, fn : ([WMSubItem]) -> [WMSubItem]) {
	\menu -> setTree(menu, section, fn(either(lookupTree(menu, section), [])))
}

addSubMenu(style: ?) -> ([?]) -> [?] {
	\items -> arrayPush(items, style)
}

removeSubMenu(style: ?) -> ([?]) -> [?] {
	\items -> filter(items, \item -> !isSameStructType(item, style))
}

replaceSubMenu(style1: ?, style2: ?) -> ([?]) -> [?] {
	\items -> items |> removeSubMenu(style1) |> addSubMenu(style2)
}

defaultWigiMenuItems : Tree<WigiMenuSection, [WMSubItem]> = makeTree()
					|> addMenuSection(WMUndoRedo(), [])
					|> addMenuSection(WMFontFamily(), [WMBasicFonts(), WMExtendedFonts()])
					|> addMenuSection(WMFontSize(), [])
					|> addMenuSection(WMAlign(), [])
					|> addMenuSection(WMFontOptions(), [WMFontSizeControls(), WMFontColorControls(), WMUnderlineControls(), WMScriptControls()])
					|> addMenuSection(WMParagraph(), [])
					|> addMenuSection(WMTools(), [WMAddMedia(), WMActiveArt(), WMSmartArt(), WMShortInteractives(), WMInsertIcon(), WMTable(), WMSpreadsheet(),
						WMCalculatedCell(), WMTemplate(), WMInsertCSV(), WMInsertDiagram(), WMNaturalMath(), WMSymbol(), WMAddHyperlink(), WMNativeWiki(),
						WMVariableInitialization(), WMAnnotatePalette(), WMShowMeStructures(), WMPastePlainText(), WMAltText(), WMHTMLResource()])
					|> addMenuSection(WMAdditional(), [WMProof(), WMSpellCheck()]);

makeWigiMenu(toolbarStyle : [WigiToolbarStyle]) -> WigiMenu {
	WigiMenu(fold(toolbarStyle, defaultWigiMenuItems, \acc, style -> {
		switch(style) {
			HideUndoRedo(): acc |> removeMenuSection(WMUndoRedo());
			HideFontSizeControl(): acc |> removeMenuSection(WMFontSize());
			MiniParagraphAlignment(): acc; // !!!!!!!!!!!!!!!!!!!!!!
			HideParagraphAlignment(): acc |> removeMenuSection(WMAlign());
			HideParagraphControl(): acc |> removeMenuSection(WMParagraph());
			HideFontOptionsControl() : acc |> removeMenuSection(WMFontOptions());
			HideTools(): acc |> removeMenuSection(WMTools());
			FullInteractiveList(): {
				acc |> processMenuSubItems(WMTools(), replaceSubMenu(WMShortInteractives(), WMFullInteractives()))
					|> processMenuSubItems(WMTools(), addSubMenu(WMInteractiveTable()))
			}
			ShowTemplateButton(): acc |> processMenuSubItems(WMTools(), removeSubMenu(WMTemplate())) |> processMenuSubItems(WMAdditional(), addSubMenu(WMTemplate()));
			DisableProof(): acc |> processMenuSubItems(WMAdditional(), removeSubMenu(WMProof()));
			SlideStripToolbarStyle(): {
				// move CoachSpeak to toolbar
				acc |> processMenuSubItems(WMTools(), removeSubMenu(WMCoachSpeak())) |> processMenuSubItems(WMAdditional(), addSubMenu(WMCoachSpeak()))
			}
			ReferenceDocument(): {
				// move AddMedia, PageBreak, PageName to toolbar
				acc |> processMenuSubItems(WMTools(), removeSubMenu(WMAddMedia())) |> processMenuSubItems(WMAdditional(), addSubMenu(WMAddMedia()))
					|> processMenuSubItems(WMTools(), removeSubMenu(WMPageBreak())) |> processMenuSubItems(WMAdditional(), addSubMenu(WMPageBreak()))
					|> processMenuSubItems(WMTools(), removeSubMenu(WMPageName())) |> processMenuSubItems(WMAdditional(), addSubMenu(WMPageName()))
			}
			SlideStyle(): {
				// move AddMedia, PageBreak, CoachSpeak to toolbar
				acc |> processMenuSubItems(WMTools(), removeSubMenu(WMAddMedia())) |> processMenuSubItems(WMAdditional(), addSubMenu(WMAddMedia()))
					|> processMenuSubItems(WMTools(), removeSubMenu(WMPageBreak())) |> processMenuSubItems(WMAdditional(), addSubMenu(WMPageBreak()))
					|> processMenuSubItems(WMTools(), removeSubMenu(WMCoachSpeak())) |> processMenuSubItems(WMAdditional(), addSubMenu(WMCoachSpeak()))
			}
			MultiProbeStyle(): {
				// move PageBreak, PageName to toolbar
				acc |> processMenuSubItems(WMTools(), removeSubMenu(WMPageBreak())) |> processMenuSubItems(WMAdditional(), addSubMenu(WMPageBreak()))
					|> processMenuSubItems(WMTools(), removeSubMenu(WMPageName())) |> processMenuSubItems(WMAdditional(), addSubMenu(WMPageName()))
			}
			KaleidoscopeStyle(): {
				// move PageBreak to toolbar
				acc |> processMenuSubItems(WMTools(), removeSubMenu(WMPageBreak())) |> processMenuSubItems(WMAdditional(), addSubMenu(WMPageBreak()))
					|> processMenuSubItems(WMTools(), removeSubMenu(WMPageName())) |> processMenuSubItems(WMAdditional(), addSubMenu(WMPageName()))
			}
			CoachMenuStyle(): {
				// remove all items from Tools section, except of NaturalMath and Symbol
				// remove extended fonts from FontFamily section
				// remove font size controls from FontOptions section
				acc |> processMenuSubItems(WMTools(), \items -> [WMNaturalMath(), WMSymbol()])
					|> processMenuSubItems(WMFontFamily(), removeSubMenu(WMExtendedFonts()))
					|> processMenuSubItems(WMFontOptions(), removeSubMenu(WMFontSizeControls()))
			}
			MathProbeMenuStyle() : {
				// remove VIE from Tools section
				acc |> processMenuSubItems(WMTools(), removeSubMenu(WMVariableInitialization()))
			}
			default: acc;
		}
	}))
}

containInMenu(menu: WigiMenu, section: WigiMenuSection) -> bool {
	containsKeyTree(menu.items, section)
}

containInSection(menu: WigiMenu, section: WigiMenuSection, subItem: WMSubItem) -> bool {
	eitherMap(
		lookupTree(menu.items, section),
		\subItems -> {
			contains(subItems, subItem)
		},
		false
	)
}