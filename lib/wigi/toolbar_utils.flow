import wigi/fontsize;
import wigi/controller;

export {
	// wigi toolbar API
	ToolbarRuntime: (
		updatingNow: ref bool,
		font: FontSizeRuntime,
		paragraphAlign: DynamicBehaviour<[WigiParagraphStyle]>,
	);
	modifyFontSize(state : WigiEditorState, m : Maybe<WigiModifyFontSize>) -> void;
	getAlignStyleCaption(a: ParaLineAlignment) -> string;

	insertWigiTableAction(isSpreadsheet : bool, type : WigiRecursiveType, state : WigiEditorState) -> void;
}

modifyFontSize(state : WigiEditorState, m : Maybe<WigiModifyFontSize>) -> void {
	switch (m) {
		Some(event): {
			selection = getValue(state.selection);
			cursor = getValue(state.cursor);
			wigiSendEvent(state, event);
			if (selection.end == cursor && selection.end != getEndPositionOfDocument(getValue(state.document), false)){
				wigiMoveCursor(state, -1, true);
			}
		}
		None(): {}
	}
}


insertWigiTableAction(isSpreadsheet : bool, type : WigiRecursiveType, state : WigiEditorState) -> void {
	switch(type) {
		WigiTable(rows, cols, cellSpans, style): {
			tightWidth = if (contains(style, WigiTableTightWidth())) [WigiCellTightWidth()] else [];
			tableName = getProperName(if (isSpreadsheet) "sheet" else "table", getValue(state.document));
			elements = if (isSpreadsheet) createWigiSpreadsheetCells(rows * cols, tightWidth) else createWigiTableCells(rows * cols, tightWidth);
			table = WigiRecursive(elements, type, [WigiName(tableName)]) |> (if (isSpreadsheet) setWigiTableCellNames else idfn);
			wigiSendEvent(state, WigiInsertElement(addEmptyLinesAroundTable(table, state))) |> ignore
		}
		default: {}
	}
}

getAlignStyleCaption(a: ParaLineAlignment) -> string {
	switch(a) {
		StartAlign(): _("Start");
		CenterAlign(): _("Center");
		EndAlign(): _("End");
		Justify(): _("Justify");
		LeftAlign(): _("Left");
		RightAlign(): _("Right");
	}
}

addEmptyLinesAroundTable (table : WigiElement, state : WigiEditorState) -> WigiElement {
	emptyText = WigiText("", []);
	emptyParagraph = WigiParagraph([WigiText("", [])], []);
	// Add TightWidth() only we are already inside some WigiRecursive
	tightWidth = if (length(getValue(state.cursor)) > 3) [TightWidth()] else [];
	makeWigiStory([emptyParagraph, WigiParagraph([emptyText, table, emptyText], tightWidth), emptyParagraph], [])
}
