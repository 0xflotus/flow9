import wigi/wigi_wiki;
import unicodecharacters;

export {
	wigiIsNumber(value : WigiValue) -> WigiValue;
	wigiIsText(value : WigiValue) -> WigiValue;
	wigiIsLogical(value : WigiValue) -> WigiValue;
	wigiIsEmpty(value : WigiValue) -> WigiValue;
	wigiIsLetter(value : WigiValue) -> WigiValue;
}

wigiIsLogical(value : WigiValue) -> WigiValue {
	WigiBool(isSameStructType(value, WigiBool(false)))
}

wigiIsNumber(value : WigiValue) -> WigiValue {
	res = switch(value : WigiValue) {
		WigiInt(__) : true;
		WigiDouble(__) : true;
		default : false;
	}
	WigiBool(res)
}

wigiIsText(value : WigiValue) -> WigiValue {
	WigiBool(isSameStructType(value, WigiString("")))
}

wigiIsEmpty(value : WigiValue) -> WigiValue {
	res = switch(value : WigiValue) {
		WigiString(v) : {
			v1 = if (isWigiContent(v)) {
				wigiStr = wigiOpenTag + v + wigiCloseTag;
				wigi2text(either(string2wigi(wigiStr, None()), makeWigiDocument("")))
			} else v;
			v1 == ""
		}
		WigiDefaultValue(): true;
		WigiBuiltInName(__): true; // not sure
		WigiArray(values): values == [];
		default : false;
	}
	WigiBool(res)
}

wigiIsLetter(value : WigiValue) -> WigiValue {
	res = switch(value : WigiValue) {
		WigiString(v): isLetterUnicode(v);
		default: false;
	}
	WigiBool(res)
}
