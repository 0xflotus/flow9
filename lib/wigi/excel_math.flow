import wigi/wigi_expr;
import wigi/excel_common;
import algorithms;
import matrix;

export {
	wigiAbs(l : [WigiValue]) -> WigiValue;
	wigiAcos(l : [WigiValue]) -> WigiValue;
	wigiAsin(l : [WigiValue]) -> WigiValue;
	wigiCos(l : [WigiValue]) -> WigiValue;
	wigiSin(l : [WigiValue]) -> WigiValue;
	wigiAtan(l : [WigiValue]) -> WigiValue;
	wigiAtan2(l : [WigiValue]) -> WigiValue;
	wigiSinh(l : [WigiValue]) -> WigiValue;
	wigiCosh(l : [WigiValue]) -> WigiValue;
	wigiAcosh(l : [WigiValue]) -> WigiValue;
	wigiAsinh(l : [WigiValue]) -> WigiValue;
	wigiAtanh(l : [WigiValue]) -> WigiValue;
	wigiFact(l : [WigiValue]) -> WigiValue;
	wigiFactDouble(l : [WigiValue]) -> WigiValue;
	wigiFloor(l : [WigiValue]) -> WigiValue;
	wigiInt(l : [WigiValue]) -> WigiValue;
	wigiPi() -> WigiValue;
	wigiPower(l : [WigiValue]) -> WigiValue;
	wigiSqrt(l : [WigiValue]) -> WigiValue;
	wigiSqrtPi(l : [WigiValue]) -> WigiValue;
	wigiMod(l : [WigiValue]) -> WigiValue;
	wigiLn(l : [WigiValue]) -> WigiValue;
	wigiLog(l : [WigiValue]) -> WigiValue;
	wigiLog10(l : [WigiValue]) -> WigiValue;
	wigiExp(l : [WigiValue]) -> WigiValue;
	wigiTrunc(l : [WigiValue]) -> WigiValue;
	wigiSign(l : [WigiValue]) -> WigiValue;
	wigiGsd(l : [WigiValue]) -> WigiValue;
	wigiLcm(l : [WigiValue]) -> WigiValue;
	wigiRand() -> WigiValue;
	wigiRandInt(l : [WigiValue]) -> WigiValue;
	wigiRandBetween(l : [WigiValue]) -> WigiValue;
	wigiTan(l : [WigiValue]) -> WigiValue;
	wigiTanh(l : [WigiValue]) -> WigiValue;
	wigiSumIf(l : [WigiValue]) -> WigiValue;
	wigiSumProduct(l : [WigiValue]) -> WigiValue;
	wigiSumSQ(l : [WigiValue]) -> WigiValue;
	wigiSumX2MY2(l : [WigiValue]) -> WigiValue;
	wigiSumX2PY2(l : [WigiValue]) -> WigiValue;
	wigiRound(l : [WigiValue]) -> WigiValue;
	wigiCombin(l : [WigiValue]) -> WigiValue;
	wigiDegrees(l : [WigiValue]) -> WigiValue;
	wigiRadians(l : [WigiValue]) -> WigiValue;
	wigiMultinominal(l : [WigiValue]) -> WigiValue;
	wigiQuotient(l : [WigiValue]) -> WigiValue;
	wigiEven(l : [WigiValue]) -> WigiValue;
	wigiOdd(l : [WigiValue]) -> WigiValue;
	wigiMRound(l : [WigiValue]) -> WigiValue;
	wigiRoundUp(l : [WigiValue]) -> WigiValue;
	wigiRoundDown(l : [WigiValue]) -> WigiValue;
	wigiSeriesSum(l : [WigiValue]) -> WigiValue;
	wigiMDeterm(l : [WigiValue]) -> WigiValue;
	wigiMMult(l : [WigiValue]) -> WigiValue;
	wigiMInverse(l : [WigiValue]) -> WigiValue;
	wigiRoman(l : [WigiValue]) -> WigiValue;
	wigiExcelValue(l : WigiValue) -> WigiValue;
}

// https://support.office.com/en-US/article/ROUND-function-C018C5D8-40FB-4053-90B1-B3E7F61A213C
wigiRound(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 2, \arg : [WigiValue] -> {
		value = wigiValue2Double(arg[0]);
		str = d2s(abs(value));
		vsign = sign(value);
		if (strContains(str, ".")) {
			len = strlen(str);
			pos = strFindFirstOf(str, ".");
			shift = trunc(wigiValue2Double(arg[1]));
			if (shift == 0) {
				WigiDouble(dround(value))
			} else if (pos + shift >= len-1) {
				arg[0]
			} else if (shift < 0) {
				if (pos + shift < 0) {
					WigiDouble(0.0)
				} else {
					strShift = if (pos + shift == 0) "0" + "." + strLeft(str, pos)
								else strLeft(str, pos+shift) + "." + substring(str, pos + shift, 1);
					valRound = s2d(strShift) |> round |> i2s;
					WigiDouble(s2d(fold(generate(0, iabs(shift), \i -> i), valRound, \a, b -> a + "0"))*vsign);
				}
			} else {
				if (pos + shift < 1) {
					WigiDouble(0.0)
				} else {
					WigiDouble(roundWithPrecision(value, shift))
				}
			}
		}
		else arg[0]
	})
}

// https://support.office.com/en-US/article/sumproduct-function-57a7bfa7-f74d-4ead-8c93-57f759c8f616
wigiSumProduct(l : [WigiValue]) -> WigiValue {
	if (length(l) != 2) WigiError("Wrong number of arguments. Expected 2, but got " + i2s(length(l)))
	else {
		wigiTemplateStatFunction([l[0]], 1, \arr1 : [WigiValue], arg1 : [WigiValue] -> {
			wigiTemplateStatFunction([l[1]], 1, \arr2 : [WigiValue], arg2 : [WigiValue] -> {
				if (length(arr1) != length(arr2)) WigiError("The length of an arrays are different")
				else WigiDouble(fold(mapi(arr1, \index, a -> wigiValue2Double(a)*wigiValue2Double(arr2[index])), 0.0, \a, b -> a+b))
			})
		})
	}
}

// https://support.office.com/en-US/article/sumif-function-468f6551-8cf3-49dc-a07c-a8f9f05d3dc2
wigiSumIf(l : [WigiValue]) -> WigiValue {
	if (length(l) != 2) WigiError("Wrong number of arguments. Expected 2, but got " + i2s(length(l)))
	else {
		wigiTemplateStatFunction([l[0]], 1, \arr : [WigiValue], arg : [WigiValue] -> {
			switch(l[1]) {
				WigiString(s) : wigiIfExp(arr, s, \a, b -> a + b, \a, b -> a);
				default : WigiString("Invalid type of the second argument")
			}
		})
	}
}

// https://support.office.com/en-US/article/LOG-function-afe9f8ad-7ace-4f74-8acb-5b4dd58e8042
wigiLog(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 2, \arg : [WigiValue] -> {
		WigiDouble(log(wigiValue2Double(arg[0]))*1.0/log(wigiValue2Double(arg[1])))
	})
}

// https://support.office.com/en-US/article/tan-function-434a3516-6069-4b1d-8aeb-9a3c458adc70
wigiTan(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		WigiDouble(sin(wigiValue2Double(arg[0]))/cos(wigiValue2Double(arg[0])))
	})
}

// https://support.office.com/en-US/article/TANH-function-017222F0-A0C3-4F69-9787-B3202295DC6C
wigiTanh(l : [WigiValue]) -> WigiValue {
	WigiDouble(wigiValue2Double(wigiSinh(l))/wigiValue2Double(wigiCosh(l)))
}

lcmA(args : [int]) -> int {
	if ( length(args) == 0 ) {
		0
	} else if ( length(args) == 1 ) {
		args[0]
	} else {
		lcm(args[0], lcmA(subrange(args, 1, length(args) - 1)))
	}
}

// https://support.office.com/en-US/article/lcm-function-18a950b2-dd2c-4b16-9947-2793eae264e7
wigiLcm(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, -1, \arg : [WigiValue] -> {
		WigiInt(lcmA(map(arg, \a -> trunc(wigiValue2Double(a)))))
	})
}

// https://support.office.com/en-US/article/gcd-function-6f8bcd7a-984e-417e-9a6a-1fcb4b460c8e
wigiGsd(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, -1, \arg : [WigiValue] -> {
		WigiInt(gcdA(map(arg, \a -> trunc(wigiValue2Double(a)))))
	})
}

// https://support.office.microsoft.com/en-US/article/RANDBETWEEN-function-4CC7F0D1-87DC-4EB7-987F-A469AB381685
wigiRandBetween(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 2, \arg : [WigiValue] -> {
		WigiDouble(random()*(wigiValue2Double(arg[1]) - wigiValue2Double(arg[0])) + wigiValue2Double(arg[0]))
	})
}

wigiRandInt(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 2, \arg : [WigiValue] -> {
		WigiInt(round(random()*(wigiValue2Double(arg[1]) - wigiValue2Double(arg[0])) + wigiValue2Double(arg[0])))
	})
}


// https://support.office.microsoft.com/en-US/article/rand-function-e98f1011-127d-4815-96f5-a26850ca1866
wigiRand() -> WigiValue {
	WigiDouble(random())
}

// https://support.office.microsoft.com/en-US/article/sign-function-cfaaf798-9b34-4569-955c-a74a523a7c4a
wigiSign(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		WigiDouble(sign(wigiValue2Double(arg[0])))
	})
}

// https://support.office.microsoft.com/en-US/article/TRUNC-function-b77557ab-c592-430a-9621-aa93dc69e5fa
wigiTrunc(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		WigiInt(trunc(wigiValue2Double(arg[0])))
	})
}

// https://support.office.microsoft.com/en-US/article/exp-function-e6154d7b-ea09-42cd-802c-fab86c4fc0e5
wigiExp(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		WigiDouble(exp(wigiValue2Double(arg[0])))
	})
}

// https://support.office.microsoft.com/en-us/article/LOG10-function-b2a4d937-50af-434d-b735-0e11f2fe623f?ui=en-US&rs=en-US&ad=US
wigiLog10(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		WigiDouble(log10(wigiValue2Double(arg[0])))
	})
}

// https://support.office.microsoft.com/en-US/article/ln-function-edbda8c7-3992-441e-baa8-63b3bb317ad5
wigiLn(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		WigiDouble(log(wigiValue2Double(arg[0])))
	})
}

// https://support.office.microsoft.com/en-US/article/mod-function-fd0c520e-e4e2-4b2b-86ba-d0569156b053
wigiMod(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 2, \arg : [WigiValue] -> {
		WigiDouble(dmod(wigiValue2Double(arg[0]),wigiValue2Double(arg[1])))
	})
}

// https://support.office.microsoft.com/en-us/article/SQRTPI-function-b5ee9236-2f60-40f1-a789-423458a2100e?ui=en-US&rs=en-US&ad=US
wigiSqrtPi(l : [WigiValue]) -> WigiValue{
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		if (wigiValue2Double(arg[0]) < 0.0) WigiError("Invalid negative number")
		else WigiDouble(sqrt(wigiValue2Double(arg[0])*PI));
	})
}

// https://support.office.microsoft.com/en-US/article/SQRT-function-aa0efaee-c686-4b5b-9a43-2a2b96abc181?CorrelationId=1fbc6cef-50e9-4567-a253-1f890fdc3ec0
wigiSqrt(l : [WigiValue]) -> WigiValue{
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		if (wigiValue2Double(arg[0]) < 0.0) WigiError("Invalid negative number")
		else WigiDouble(sqrt(wigiValue2Double(arg[0])));
	})
}

// https://support.office.microsoft.com/en-US/article/power-function-bad07d5a-d787-48a5-9e72-853a5eb0e515
wigiPower(l : [WigiValue]) -> WigiValue{
	wigiTemplateMathFunction(l, 2, \arg : [WigiValue] -> {
		WigiDouble(dpow(wigiValue2Double(arg[0]), wigiValue2Double(arg[1])));
	})
}

// https://support.office.microsoft.com/en-US/article/pi-function-6da322ac-b7dc-4c41-b698-4f00b234f2bc
wigiPi() -> WigiValue {
	WigiDouble(PI);
}

// https://support.office.com/en-us/article/int-function-a6c4af9e-356d-4369-ab6a-cb1fd9d343ef
wigiInt(l : [WigiValue]) -> WigiValue {
	len = length(l);
	if (len != 1) {
		wrongNumberOfArgumentsError(len, 1)
	} else {
		val = l[0];
		switch (val) {
			WigiString(v): if (isDigits(v)) WigiInt(wigiValue2Int(val)) else WigiError("Invalid type of arguments. [16]");
			default: WigiInt(wigiValue2Int(val));
		}
	}
}

// https://support.office.microsoft.com/en-US/article/FLOOR-function-4190c160-f465-41f4-9faf-197538d1a150
wigiFloor(l : [WigiValue]) -> WigiValue{
	wigiTemplateMathFunction(l, 2, \arg : [WigiValue] -> {
		WigiDouble(floorTo(wigiValue2Double(arg[0]),wigiValue2Double(arg[1])))
	})
}

factDouble(n) {
	if (n <= 1) 1
	else n * factDouble(n-2)
}

// https://support.office.microsoft.com/en-US/article/FACTDOUBLE-function-E67697AC-D214-48EB-B7B7-CCE2589ECAC8
wigiFactDouble(l : [WigiValue]) -> WigiValue{
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		if (wigiValue2Double(arg[0]) < 0.0) WigiError("Invalid negative number")
		else WigiInt(factDouble(trunc(wigiValue2Double(arg[0]))))
	})
}

// https://support.office.microsoft.com/en-US/article/FACT-function-d01e2fca-70c0-42e1-86b5-62b4cbe2545b
wigiFact(l : [WigiValue]) -> WigiValue{
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		if (wigiValue2Double(arg[0]) < 0.0) WigiError("Invalid negative number")
		else WigiInt(factorial(trunc(wigiValue2Double(arg[0]))))
	})
}

// https://support.office.microsoft.com/en-US/article/atan2-function-c25c8ffc-a96a-4707-afa6-448f59b7ba94
wigiAtan2(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 2, \arg : [WigiValue] -> {
		WigiDouble(atan2(wigiValue2Double(arg[0]),wigiValue2Double(arg[1])))
	})
}

// https://support.office.microsoft.com/en-US/article/ATAN-function-8060a7d8-5d3e-4db6-b951-021fe3445fd1
wigiAtan(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		WigiDouble(atan(wigiValue2Double(arg[0])))
	})
}

// https://support.office.microsoft.com/en-US/article/sin-function-52bd4942-a23b-4349-ad98-fdc04c1a4ba3
wigiSin(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		WigiDouble(sin(wigiValue2Double(arg[0])))
	})
}

// https://support.office.microsoft.com/en-US/article/COS-function-0FB808A5-95D6-4553-8148-22AEBDCE5F05
wigiCos(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		WigiDouble(cos(wigiValue2Double(arg[0])))
	})
}

// https://support.office.microsoft.com/en-US/article/asin-function-ed0a09b6-9980-45b0-8fd4-69aec1e6476f
wigiAsin(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		WigiDouble(asin(wigiValue2Double(arg[0])))
	})
}

// https://support.office.microsoft.com/en-US/article/acos-function-f56c7610-3437-4f0e-9744-63f3587367f1
wigiAcos(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		WigiDouble(acos(wigiValue2Double(arg[0])))
	})
}

// https://support.office.microsoft.com/en-US/article/sinh-function-23dcaac8-57f8-49dd-93b8-3befe1b62136
wigiSinh(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		x = wigiValue2Double(arg[0]);
		WigiDouble((exp(x)-exp(-x))/2.0)
	})
}

// https://support.office.microsoft.com/en-US/article/cosh-function-40252700-9f1f-4a04-b3ac-bae00960d459
wigiCosh(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		x = wigiValue2Double(arg[0]);
		WigiDouble((exp(x)+exp(-x))/2.0)
	})
}

// https://support.office.microsoft.com/en-US/article/acosh-function-0db4050f-fa8f-4765-9747-a3d02c48f8d7
wigiAcosh(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		x = wigiValue2Double(arg[0]);
		if(x >= 1.0) WigiDouble(log(x + sqrt(dpow(x, 2.0) - 1.0)))
		else WigiError("Invalid argument")
	})
}

// https://support.office.microsoft.com/en-US/article/asinh-function-e57edadd-1104-427d-9dde-d1d1f2c5e098
wigiAsinh(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		x = wigiValue2Double(arg[0]);
		WigiDouble(log(x + sqrt(dpow(x, 2.0) + 1.0)));
	})
}

// https://support.office.microsoft.com/en-US/article/ATANH-function-3947779e-712c-4792-8f2d-4a477e8c66f3
wigiAtanh(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		x = wigiValue2Double(arg[0]);
		if(x > -1.0 && x < 1.0) WigiDouble(log((1.0+x)/(1.0-x))/2.0)
		else WigiError("Invalid argument")
	})
}

// https://support.office.microsoft.com/en-US/article/abs-function-79ea29ce-58d3-455a-a3e2-493a4eda1a98
wigiAbs(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		WigiDouble(abs(wigiValue2Double(arg[0])))
	})
}

// https://support.office.microsoft.com/en-US/article/combin-function-7a7fe84a-f4d8-4eaa-bb0a-d52427ffea4b
wigiCombin(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 2, \arg : [WigiValue] -> {
		n = wigiValue2Int(arg[0]); // The number of items.
		k = wigiValue2Int(arg[1]); // The number of items in each combination.

		if (n < 0 || k < 0 || n < k) {
			WigiError("Invalid arguments")
		} else {
			WigiInt(factorial(n)/(factorial(k)*factorial(n-k)))
		}
	})
}

// https://support.office.microsoft.com/en-US/article/degrees-function-d68c59c9-1aec-4c14-8e68-0bb8d9c10048
wigiDegrees(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		WigiDouble(wigiValue2Double(arg[0])*180.0/PI)
	})
}

// https://support.office.microsoft.com/en-US/article/radians-function-5bd51776-baaa-49f3-97eb-b7123a2df3d3
wigiRadians(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		WigiDouble(wigiValue2Double(arg[0])*PI/180.0)
	})
}

// https://support.office.microsoft.com/en-US/article/sumsq-function-8484dc40-06d1-4b59-955e-70c84e3b830c
wigiSumSQ(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, -1, \arg : [WigiValue] -> {
		WigiDouble(fold(arg, 0.0, \a,b -> a+dpow(wigiValue2Double(b),2.0)))
	})
}

// https://support.office.microsoft.com/en-US/article/sumx2my2-function-603b548d-f3e7-4043-8ec2-345c044f681a
wigiSumX2MY2(l : [WigiValue]) -> WigiValue {
	wigiTemplateStatFunction2([l[0]], 1, \arr1 : [WigiValue], arg1 : [WigiValue] -> {
		wigiTemplateStatFunction2([l[1]], 1, \arr2 : [WigiValue], arg2 : [WigiValue] -> {
			if (length(arr1) != length(arr2)) WigiError("The length of an arrays are different")
			else {
				WigiDouble(foldi(arr1, 0.0, \index,a,b -> {
					a + dpow(wigiValue2Double(b),2.0) - dpow(wigiValue2Double(arr2[index]),2.0)
				}))
			}
		})
	})
}

// https://support.office.microsoft.com/en-US/article/sumx2py2-function-2f4aa1ae-eb09-43a0-980b-a84b261f6b6f
wigiSumX2PY2(l : [WigiValue]) -> WigiValue {
	wigiTemplateStatFunction2([l[0]], 1, \arr1 : [WigiValue], arg1 : [WigiValue] -> {
		wigiTemplateStatFunction2([l[1]], 1, \arr2 : [WigiValue], arg2 : [WigiValue] -> {
			if (length(arr1) != length(arr2)) WigiError("The length of an arrays are different")
			else {
				WigiDouble(foldi(arr1, 0.0, \index,a,b -> {
					a + dpow(wigiValue2Double(b),2.0) + dpow(wigiValue2Double(arr2[index]),2.0)
				}))
			}
		})
	})
}

// https://support.office.microsoft.com/en-US/article/multinomial-function-2f152dde-8e38-4b49-9048-95d76c48ab82
wigiMultinominal(l : [WigiValue]) -> WigiValue {
	wigiTemplateIntFunction(l, -1, \arr : [WigiValue] -> {
		if(forall(l, \v -> wigiValue2Int(v) > 0)) {
			WigiInt(factorial(fold(l, 0, \a,b -> a + wigiValue2Int(b)))/
					fold(l, 1, \a,b -> a*factorial(wigiValue2Int(b))));
		}
		else WigiError("Invalid arguments")
	})
}

// https://support.office.microsoft.com/en-US/article/QUOTIENT-function-33a8b6f4-dd7a-44c6-b745-bcf823f2f090
wigiQuotient(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 2, \arg : [WigiValue] -> {
		WigiInt(trunc(wigiValue2Double(arg[0])/wigiValue2Double(arg[1])))
	})
}

// https://support.office.microsoft.com/en-US/article/even-function-2d0bb29b-4664-412f-9887-df4b310aa3e4
wigiEven(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		val = wigiValue2Double(arg[0]);
		x = trunc(val);
		shift = if (abs(val) > abs(i2d(x))) 1 else 0;
		signx = if(x < 0) -1 else 1;
		res = x+shift*signx;
		WigiInt(if(res%2 == 0) res else res + signx)
	})
}

// https://support.office.microsoft.com/en-US/article/ODD-function-DEAE64EB-E08A-4C88-8B40-6D0B42575C98
wigiOdd(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		val = wigiValue2Double(arg[0]);
		x = trunc(val);
		shift = if (abs(val) > abs(i2d(x))) 1 else 0;
		signx = if(x < 0) -1 else 1;
		res = x+shift*signx;
		WigiInt(if(res%2 != 0) res else res + signx)
	})
}

// https://support.office.microsoft.com/en-US/article/MROUND-function-C299C3B0-15A5-426D-AA4B-D2D5B3BAF427
wigiMRound(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 2, \args : [WigiValue] -> {
		number = wigiValue2Double(args[0]);
		num_digits = wigiValue2Double(args[1]);
		WigiDouble(dround(number / num_digits) * num_digits)
	})
}

// https://support.office.microsoft.com/en-US/article/ROUNDDOWN-function-2EC94C73-241F-4B01-8C6F-17E6D7968F53
wigiRoundDown(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 2, \args : [WigiValue] -> {
		number = wigiValue2Double(args[0]);
		num_digits = wigiValue2Double(args[1]);
		precision = dpow(10.0, -num_digits);
		WigiDouble(floorTo(number, precision))
	})
}

// https://support.office.microsoft.com/en-US/article/roundup-function-75ccc6e2-285a-4995-a012-890c22903444
wigiRoundUp(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 2, \args : [WigiValue] -> {
		number = wigiValue2Double(args[0]);
		num_digits = wigiValue2Double(args[1]);
		precision = dpow(10.0, -num_digits);
		WigiDouble(roundTo(sign(number) * (abs(number) + precision / 2.0), precision))
	})
}

// https://support.office.microsoft.com/en-US/article/seriessum-function-45aa1045-2090-4606-8661-38c6833f4e7f
wigiSeriesSum(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(subrange(l,0,3), 3, \arg : [WigiValue] -> {
		wigiTemplateStatFunction2([l[3]], 1, \arr : [WigiValue], arg2 : [WigiValue] -> {
			x = wigiValue2Double(arg[0]);
			n = wigiValue2Double(arg[1]);
			m = wigiValue2Double(arg[2]);
			WigiDouble(foldi(arr, 0.0, \idx, a, b -> {
				a + wigiValue2Double(b)*dpow(x, n+i2d(idx)*m)
			}))
		})
	})
}

// https://support.office.com/en-nz/article/MDETERM-function-58714c00-0d0d-4b27-9bb3-fc06766e899b
wigiMDeterm(l : [WigiValue]) -> WigiValue {
	wigiTemplateMatrixFunction(l, 1, \arr : [WigiValue], arg2 : [WigiValue] -> {
		arr_ = map(arr, \a -> {
			switch(a) {
				WigiArray(ar) : map(ar, \v -> wigiValue2Double(v));
				default : [];
			}
		});

		colCount = length(arr_);
		rowCount = length(arr_[0]);
		if (colCount != rowCount) WigiError("Matrix does not have an equal number of rows and columns")
		else WigiDouble(findDeterminant(arr_))
	})
}

// https://support.office.com/en-in/article/MMULT-function-40593ed7-a3cd-4b6b-b9a3-e4ad3c7245eb
wigiMMult(l : [WigiValue]) -> WigiValue {
	wigiTemplateMatrixFunction([l[0]], 1, \arr1 : [WigiValue], arg1 : [WigiValue] -> {
		wigiTemplateMatrixFunction([l[1]], 1, \arr2 : [WigiValue], arg2 : [WigiValue] -> {

			wigiArray2DoubleArr = \m : [WigiValue] -> {
				map(m, \a -> {
					switch(a) {
						WigiArray(ar) : map(ar, \v -> wigiValue2Double(v));
						default : [];
					}
				});
			}
			matrix1 = wigiArray2DoubleArr(arr1);
			matrix1ColCount = length(matrix1[0]);
			matrix2 = wigiArray2DoubleArr(arr2);
			matrix2RowCount = length(matrix2);
			if(matrix1ColCount != matrix2RowCount) WigiError("The number of columns in Matrix1 is different from the number of rows in Matrix2")
			else WigiString(fold(multiplyMatrices(matrix1, matrix2), "", \acc, b -> acc + "\n" + trim2(fold(b, "", \str, d -> str + d2s(d) + ", "), ", ")))
		})
	})
}

// https://support.office.com/en-nz/article/MINVERSE-function-11f55086-adde-4c9f-8eb9-59da2d72efc6
wigiMInverse(l : [WigiValue]) -> WigiValue {
	wigiTemplateMatrixFunction(l, 1, \arr : [WigiValue], arg2 : [WigiValue] -> {
		arr_ = map(arr, \a -> {
			switch(a) {
				WigiArray(ar) : map(ar, \v -> wigiValue2Double(v));
				default : [];
			}
		});

		colCount = length(arr_);
		rowCount = length(arr_[0]);
		if (colCount != rowCount) WigiError("Matrix does not have an equal number of rows and columns")
		else WigiString(fold(inverseMatrix(arr_), "", \acc, b -> acc + "\n" + trim2(fold(b, "", \str, d -> str + d2s(d) + ", "), ", ")))
	})
}

getRoman(value : double) -> string {

	roman = [
				Pair(1, "I"),
				Pair(2, "II"),
				Pair(3, "III"),
				Pair(4, "IV"),
				Pair(5, "V"),
				Pair(6, "VI"),
				Pair(7, "VII"),
				Pair(8, "VIII"),
				Pair(9, "IX"),
				Pair(10, "X"),
				Pair(20, "XX"),
				Pair(30, "XXX"),
				Pair(40, "XL"),
				Pair(50, "L"),
				Pair(60, "LX"),
				Pair(70, "LXX"),
				Pair(80, "LXXX"),
				Pair(90, "XC"),
				Pair(100, "C"),
				Pair(200, "CC"),
				Pair(300, "CCC"),
				Pair(400, "CD"),
				Pair(500, "D"),
				Pair(600, "DC"),
				Pair(700, "DCC"),
				Pair(800, "DCCC"),
				Pair(900, "CM"),
				Pair(1000, "M"),
				Pair(2000, "MM"),
				Pair(3000, "MMM")
			];

	val = trunc(value);
	if(val < 1 || val > 3999) ""
	else {
		str = i2s(val);
		len = strlen(str);
		arr = generate(0, len, \i -> rpad(getCharAt(str, i), "0", len-i));

		map(arr, \a -> {
				idx = findi(roman, \r -> r.first == s2i(a));
				switch (idx) {
					Some(i) : roman[i].second;
					None() : "";
				}
		}) |> concatStrings
	}
}

wigiRoman(l : [WigiValue]) -> WigiValue {
	wigiTemplateMathFunction(l, 1, \arg : [WigiValue] -> {
		res = getRoman(wigiValue2Double(arg[0]));
		if (res == "") WigiError("Invalid number")
		else WigiString(res)
	})
}

wigiExcelValue(v : WigiValue) -> WigiValue {
	switch(v) {
		WigiString(text): if (i2s(s2i(text)) == text) WigiInt(s2i(text)) else WigiError("#VALUE!");
		WigiInt(__): v;
		WigiDouble(__): v;
		default: WigiError("#VALUE!")
	}
}
