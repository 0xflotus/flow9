import wigi/wigi_engine;

export {
	setServiceNamespaceCell(cell : WigiCellName) -> void;

	calculateElementBoolFormula(name: string, content : Tree<string, string>, engine : WigiEngine, wigiName : WigiCellName, preview : bool) -> Disposable<Behaviour<bool>>;

	// Find in WigiEnvironment namespaceCell and construct namespaces array from its value.
	// scopeCell's value should be WigiString or WigiArray of WigiString-s
	getNamespaceCell(engine : WigiEngine, cell : WigiCellName) -> [string];

	getDefaultServiceScope(env : WigiEnvironment) -> WigiScope;
	// Set the default scope to some children scope of global service scope. subScope is the path, not the final scope
	setDefaultServiceScope(env : WigiEnvironment, subScope : WigiScope) -> void;
}

defaultServiceNamespaceCell = ref WigiCellName("", globalWigiNamespace);

setServiceNamespaceCell(cell : WigiCellName) -> void {
	defaultServiceNamespaceCell := cell;
}

calculateElementBoolFormula(name: string, content : Tree<string, string>, engine : WigiEngine, wigiName : WigiCellName, preview : bool) -> Disposable<Behaviour<bool>> {
	if (preview) {
		visible_formula = lookupTreeDef(content, name, "=true");
		parsed = parseWigiExpr(checkOnEqualsSign(visible_formula));
		if (isWigiValue(parsed)) {
			v = cast(parsed : WigiExpr -> WigiValue);
			Disposable(v |> wigiValue2Bool |> const, nop)
		} else {
			stateScope = getDefaultStateScope(^(engine.env));
			visibleScope = concat([name], getNamespaceCell(engine, ^defaultServiceNamespaceCell)) |> namespace2scope;

			res = addFormulaToEngine(engine, visibleScope, wigiName.name, stateScope, parsed, true, false);
			vis = make(true);
			uns = subscribe(res.first, \v -> nextDistinct(vis, wigiValue2Bool(v)));
			Disposable(vis, \ -> {
				applyall(res.second);
				uns();
			})
		}
	} else Disposable(const(true), nop)
}

getNamespaceCell(engine : WigiEngine, cell : WigiCellName) -> [string] {
	getNamespaceCellFromWigiEnvironment(^(engine.env), cell)
}

getDefaultServiceScope(env : WigiEnvironment) -> WigiScope {
	namespace2scope(getNamespaceCellFromWigiEnvironment(env, ^defaultServiceNamespaceCell))
}

setDefaultServiceScope(env : WigiEnvironment, scope : WigiScope) -> void {
	setDefaultScope(env, ^defaultServiceNamespaceCell, scope);
}
