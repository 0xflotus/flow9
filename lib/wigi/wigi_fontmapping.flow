import wigi/events;
import ui/fontmapping;

export {
	//  Wigi font mapping functions
	//
	//  By default Wigi uses concrete font families for text styles instead of attributes.
	//  "Italic"  for italic text, "Medium" for bold and "MediumItalic" for bold italic.
	//  To introduce the new fonts one should use the font mapping.

	// This function sets up the font map
	// Example:
	// setWigiFontMapping([
	// 	Pair("Italic", "ProximaSemiItalic"),
	// 	Pair("Medium", "ProximaExtraBold"),
	// 	Pair("MediumItalic", "ProximaExtraItalic"),
	// ]);
	setWigiFontMapping(fontMap : [Pair<string, string>]) -> void;

	// This function clears the font map.
	clearWigiFontMapping() -> void;

	// This function translates the font family name according the font map.
	// Called from getCharacterStyleFromWigiTextStyle(), which covers most of
	// wigi rendering needs including editors.
	translateWigiFontFamily(string) -> string;

	//Enable/disable font mapping - for some objects (like inactive art it breaks layout)
	//see http://process.area9.dk/cases/default.asp?44118
	enableWigiFontMapping() -> void;
	disableWigiFontMapping() -> void;

	switchWigiMappedFont(fontFamily : string, style : FontFamilyStyle, turnOn : Maybe<bool>) -> string;
}

skinFontMap : ref Tree<string, string> = ref makeTree();
isWigiFontMappingEnabled = ref true;

enableWigiFontMapping() {
	isWigiFontMappingEnabled := true;
}

disableWigiFontMapping() {
	isWigiFontMappingEnabled := false;
}

setWigiFontMapping(fontMap) {
	skinFontMap := pairs2tree(fontMap);
}

clearWigiFontMapping() {
	skinFontMap := makeTree();
}

translateWigiFontFamily(family) {
	if (^isWigiFontMappingEnabled)
		lookupTreeDef(^skinFontMap, family, family)
	else
		family
}

switchWigiMappedFont(fontFamily : string, style : FontFamilyStyle, turnOn : Maybe<bool>) -> string {
	family = getReverseMappedFontFamily(fontFamily);  
	eitherTurnOn = \on, off, def -> eitherMap(turnOn, \v -> if (v) on else off, def);

	newFamily = switch (style : FontFamilyStyle) {
		SBold(): {
			if (endsWith(family, "Medium") || endsWith(family, "Bold")) eitherTurnOn(family, "Book", "Book")
			else if (endsWith(family, "MediumItalic") || endsWith(family, "BoldItalic")) eitherTurnOn(family, "Italic", "Italic")
			else if (endsWith(family, "Italic")) eitherTurnOn("MediumItalic", family, "MediumItalic")
			else eitherTurnOn("Medium", family, "Medium")
		}
		SItalic(): {
			if (family == "Italic") eitherTurnOn(family, "Book", "Book")
			else if (family == "Medium") eitherTurnOn("MediumItalic", family, "MediumItalic")
			else if (family == "MediumItalic") eitherTurnOn(family, "Medium", "Medium")
			else if (family == "DejaVuSans") eitherTurnOn("DejaVuSansOblique", family, "DejaVuSansOblique")
			else if (family == "DejaVuSansOblique") eitherTurnOn(family, "DejaVuSans", "DejaVuSans")
			else if (family == "Minion") eitherTurnOn("MinionItalics", family, "MinionItalics")
			else if (family == "MinionItalics") eitherTurnOn(family, "Minion", "Minion")
			else eitherTurnOn("Italic", family, "Italic")
		}
	}

	getMappedFont(newFamily, 0.0).first
}
