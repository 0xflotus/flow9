import wigi/position;

export {
	// TODO: It is worth it to consider supporting multiple selections.
	// See case http://process.area9.dk/cases/default.asp?34392
	WigiSelection : (start : [int], end : [int]);

	wigiClearSelection(document : WigiElement) -> WigiSelection;
	isEmptySelection(s : WigiSelection) -> bool;
	makeSelection(doc : WigiElement, anchor : [int], cursor : [int]) -> WigiSelection;
	selection2string(doc : WigiElement, s : WigiSelection) -> string;

	isInsideRegularWigiSelection(selection : WigiSelection, elementPath : [int], element : WigiElement) -> bool;
}

wigiClearSelection(doc : WigiElement) -> WigiSelection {
	s = getStartPositionOfDocument(doc);
	WigiSelection(s, s);
}

isEmptySelection(s : WigiSelection) -> bool {
	s.start == s.end;
}

makeSelection(doc : WigiElement, anchor : [int], cursor : [int]) -> WigiSelection {
	c = compareDocumentPosition(anchor, cursor);
	if (c == 0) {
		WigiSelection(anchor, cursor);
	} else if (c < 0) {
		WigiSelection(anchor, cursor);
	} else {
		WigiSelection(cursor, anchor);
	}
}

selection2string(doc : WigiElement, s : WigiSelection) -> string {
	if (isEmptySelection(s)) {
		"None";
	} else {
		elementPosition2string(doc, s.start) + " - " + elementPosition2string(doc, s.end);
	}
}

// Checks if inside regular (i.e. not rectangular) selection
isInsideRegularWigiSelection(selection : WigiSelection, elementPath : [int], element : WigiElement) -> bool {
	startlen = length(selection.start);
	endlen = length(selection.end);
	pathlen = length(elementPath);

	minlen = min3(startlen, endlen, pathlen);

	path = subrange(elementPath, 0, minlen);
	start = subrange(selection.start, 0, minlen);
	end = subrange(selection.end, 0, minlen);

	// Check if selection starts before the element or at the begining of the element
	(compareDocumentPosition(path, start) > 0 || isPositionParentGeneral(selection.start, concat(elementPath, getStartPositionOfDocument(element)))) &&
	// and ends after the element or at the end of the element
	(compareDocumentPosition(path, end) < 0 || compareDocumentPosition(concat(elementPath, getEndPositionOfDocument(element, false)), selection.end) == 0)
}
