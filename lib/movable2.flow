
import form;
import behaviour;
import movable;  // makeMovable, MovableSite, MovableXYWithRestriction
import gui;
import interactiveutils; // absorbMouseEvents

export {

	// Makes a window that can be dragged by its caption
	makeWindow(content : Form, caption : string) -> Form;
	// Makes a window that can be dragged by its caption and you can control it position
	makeWindowXY(x : DynamicBehaviour<double>, y : DynamicBehaviour<double>, content : Form, caption : string) -> Form;

	// This is a resizableWindow, you can control its position and size
	resizableWindowXY(x : DynamicBehaviour<double>, y : DynamicBehaviour<double>, width : DynamicBehaviour<double>, height : DynamicBehaviour<double>, minSize : DynamicBehaviour<WidthHeight>, caption : string, content : Form) -> Form;

}


// This is a resizableWindow, you can control its position and size
resizableWindowXY(x : DynamicBehaviour<double>, y : DynamicBehaviour<double>, width : DynamicBehaviour<double>, height : DynamicBehaviour<double>, minSize : DynamicBehaviour<WidthHeight>, caption : string, content : Form) -> Form {
	awh = makeWH();
	minWidth = select(minSize, \_minSize -> if (_minSize.width < 16.0) 16.0 else _minSize.width);
	minHeight = select(minSize, \_minSize -> if (_minSize.height < 16.0) 16.0 else _minSize.height);
	form = {
		Group([
			Available(width, height, Crop(make(0.0), make(0.0), width, height, content)),
			Offset(- 16.0, - 16.0,
				MovableXYWithRestriction(
					width,
					height,
					minWidth,
					minHeight,
					select5(awh, x, y, minWidth, minHeight, \_awh, _x, _y, _minWidth, _minHeight -> {
						WidthHeight(_awh.width - (_x + _minWidth - 16.0), _awh.height - (_y + _minHeight - 16.0 + 30.0));
					}),
					Picture("images/expand.png", [])
				)
			)
		])
	};
	Inspect([IAvailable(awh)], Cursor(ArrowCursor(), absorbMouseEvents(makeWindowXY(x, y, Background([Fill(0xF0F0F0)], form), caption))))
}

// Makes a window that can be dragged by its caption
makeWindow(content, caption) {
	x = make(0.0);
	y = make(0.0);

	enabled = make(true);

	captionForm = makeMovable(MovableSite(Translate(x, y, makeCaption(caption)), x, y, enabled), [DragDrop()]);

	HeaderFooter(
		captionForm,
		Translate(x, y, content),
		Empty()
	)
}

// Makes a window that can be dragged by its caption and you can control it position
makeWindowXY(x, y, content, caption) {
	enabled = make(true);

	captionForm = makeMovable(MovableSite(Translate(x, y, makeCaption(caption)), x, y, enabled), [DragDrop()]);

	HeaderFooter(
		captionForm,
		Translate(x, y, content),
		Empty()
	)
}

makeCaption(caption: string) {
	Behind(\w, h -> {
		topRoundedRect(w, h, 10.0, [Fill(0x043543)])
	}, Align(0.5, 0.0, Border(0.0, 6.0, 0.0, 6.0, Text(caption, [FontSize(14.0), Fill(0xffffff)]))))
}

