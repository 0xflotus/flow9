import formats/json/json;
import pretty;

export {
	// It is probably better to use json2stringFormatted instead of this. 
	json2doc(j : Json) -> Doc;
}

json2doc(j : Json) -> Doc {
	switch(j) {
		JsonNull(): textDoc("null");
		JsonBool(v): textDoc(b2s(v));
		JsonDouble(d): textDoc(d2s(d));
		JsonString(s): textDoc(toString(s));
		JsonArray(arr): {
			catDocs([
				textDoc("["),
				nestDoc(1, lineDoc()),
				nestDoc(1,
					catDocs(interleave(
						map(arr, json2doc),
						catDoc(textDoc(","), lineDoc())
					))
				),
				lineDoc(),
				textDoc("]")
			])
		}
		JsonObject(members): {
			numberOfMembers = length(members);
			catDocs([
				textDoc("{"),
				nestDoc(1, lineDoc()),
				nestDoc(1,
					catDocs(interleave(
						mapi(members, \idx, m -> {
							catDocs([
								textDoc(toString(m.first)),
								textDoc(": "),
								json2doc(m.second),
								if (idx < numberOfMembers - 1) textDoc(",") else NilDoc()
							])
						}),
						lineDoc()
					))
				),
				lineDoc(),
				textDoc("}"),
			])
		}
	}
}