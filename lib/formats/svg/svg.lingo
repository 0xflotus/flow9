svg_path = (  wsps moveto:m wsps draw_commands:cmds closepath:z wsps { concat(:m, concat(:cmds, :z)) }
             | wsps draw_commands:cmds closepath:z wsps { concat(:cmds, :z) }
             |  wsps moveto:m draw_commands:cmds  { concat(:m, :cmds) }         
    )+ | wsps draw_commands_not_empty:cmds { array(:cmds) };

draw_commands = (command:c wsps {:c})*:cmds { concatA(:cmds) };
draw_commands_not_empty = (command:c wsps {:c})+:cmds { concatA(:cmds) };

command = lineto | horizontal_lineto | vertical_lineto
    | curveto | smooth_curveto | quadratic_curveto | smooth_quadratic_curveto
    | elliptical_arc;

closepath = ('Z' | 'z') { array(ClosePath()) };

// Commands are cached to avoid multiple calls of peg actions with side effects
moveto! =  'M' (wsps number$x comma_wsp number$y { move($x, $y) })+
        |  'm' (wsps number$dx comma_wsp number$dy { moveRel($dx, $dy) })+ ;

lineto! = 'L' (wsps number$x comma_wsp number$y { line($x, $y) })+
        |  'l' (wsps number$dx comma_wsp number$dy { lineRel($dx, $dy) })+ ;

horizontal_lineto! = 'H' (wsps number$x { h($x) })+
        |  'h' (wsps number$dx { hRel($dx) })+;

vertical_lineto! = 'V' (wsps number$y { v($y) })+
        |  'v' (wsps number$dy { vRel($dy) })+ ;

curveto! =
    'C' (wsps number$cx1 comma_wsp number$cy1 comma_wsp number$cx2 comma_wsp number$cy2 comma_wsp number$x comma_wsp number$y { cubicBezier($x, $y, $cx1, $cy1, $cx2, $cy2) } )+:c { concatA(:c) }
    | 'c' (wsps number$cx1 comma_wsp number$cy1 comma_wsp number$cx2 comma_wsp number$cy2 comma_wsp number$x comma_wsp number$y { cubicBezierRel($x, $y, $cx1, $cy1, $cx2, $cy2) } )+:c { concatA(:c) };

smooth_curveto! =
    'S' (wsps number$cx2 comma_wsp number$cy2 comma_wsp number$x comma_wsp number$y { smoothBezier($x, $y, $cx2, $cy2) })+:s { concatA(:s) }
    | 's' (wsps number$cx2 comma_wsp number$cy2 comma_wsp number$x comma_wsp number$y { smoothBezierRel($x, $y, $cx2, $cy2) })+:s { concatA(:s) };

quadratic_curveto! =
    'Q' (wsps number$cx1 comma_wsp number$cy1 comma_wsp number$x comma_wsp number$y { quadBezier($x, $y, $cx1, $cy1) } )+
    | 'q' (wsps number$cx1 comma_wsp number$cy1 comma_wsp number$x comma_wsp number$y { quadBezierRel($x, $y, $cx1, $cy1) } )+;

smooth_quadratic_curveto! =
    'T' (wsps number$x comma_wsp number$y { smoothQuadBezier($x, $y) } )+
    | 't' (wsps number$x comma_wsp number$y { smoothQuadBezierRel($x, $y) } )+;

elliptical_arc! =
    'A' (wsps number$rx comma_wsp number$ry comma_wsp 
        number$xrot comma_wsp flag$f1 comma_wsp flag$f2 comma_wsp number$x comma_wsp number$y { arc($rx, $ry, $xrot, $f1, $f2, $x, $y) } )+:a { concatA(:a) }
    | 'a' (wsps wsps number$rx comma_wsp number$ry comma_wsp 
        number$xrot comma_wsp flag$f1 comma_wsp flag$f2 comma_wsp number$x comma_wsp number$y { arcRel($rx, $ry, $xrot, $f1, $f2, $x, $y) } )+:a { concatA(:a) };

number! =  sign? digit_sequence '.' digit_sequence | sign?  digit_sequence '.' | sign? '.' digit_sequence | sign? digit_sequence;
sign = '+' | '-';
digit_sequence = ('0'-'9')+;
flag = '0'-'1';
comma_wsp = wsps ','? wsps;
wsp = ' ' | '\u0009'| '\u000D' | '\u000A';
wsps = wsp*;
