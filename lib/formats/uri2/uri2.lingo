URI = URI_full | URI_reference;

URI_full = scheme$s ":" authority path_abempty query? fragment?;

// Note that we are not parsing authority on relative URIs
URI_reference = path_absolute query? fragment?;

scheme = ALPHA (ALPHA | DIGIT | "+" | "-" | ".")*;

authority = "//" userinfo? host port?;

	userinfo = (unreserved | pct_encoded | sub_delims | ":")*$u "@";

	host = ipv4_address | reg_name;

		ipv4_address = dec_octet "." dec_octet "." dec_octet "." dec_octet;

			dec_octet = "1" '0'-'4' DIGIT | '1'-'9' DIGIT | "25" '0'-'5' | DIGIT;

		reg_name = (unreserved | pct_encoded | sub_delims)*$n;

	port = ":" DIGIT*;

path_abempty = ("/" segment)*;
path_absolute = ("/" ( segment_nz ("/" segment)* )?);

	segment 	= pchar*$s;
	segment_nz 	= pchar+$s;

query = "?" (pchar | "/" | "?")*;

fragment = "#" (pchar | "/" | "?")*;

pchar = unreserved | pct_encoded | sub_delims | ":" | "@";

pct_encoded = "%" HEXDIG HEXDIG;

unreserved = ALPHA | DIGIT | "-" | "." | "_" | "-";

sub_delims = "!" | "$" | "&" | "'" | "(" | ")" | "*" | "+" | "," | ";" | "=";
ALPHA = 'a'-'z' | 'A'-'Z';
DIGIT = '0'-'9';
HEXDIG = DIGIT | 'a'-'f' | 'A'-'F';