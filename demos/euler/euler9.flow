import runtime;

/*
A Pythagorean triplet is a set of three natural numbers, a < b < c, for which,

a² + b² = c²
For example, 3² + 4² = 9 + 16 = 25 = 52.

There exists exactly one Pythagorean triplet for which a + b + c = 1000.
Find the product abc.
*/

/*
Some algebra:
c = 1000 - b - a
a² + b² = (1000 - b - a)²
a² + b² = a² + 2 a b - 2000 a + b² - 2000 b + 1000000
0 = 2 a b - 2000 a - 2000 b + 1000000
b = (1000 (a - 500))/(a - 1000) and a!=1000

In other words, once we have a, we can derive b and c.
*/

euler9(a : int, biggest : int) -> int {
	if (a < 1000) {
		b = (1000 * (a - 500))/(a - 1000);
		c = 1000 - b - a;
		nbiggest = if (b < 0 || c < 0) {
			biggest;
		} else {
			if (a*a + b * b == c * c) {
				product = a * b * c;
				max(product, biggest);
			} else biggest;
		}
		euler9(a + 1, nbiggest);
	} else biggest;
}

main() {
	println(euler9(1, 0)); // Correct: 31875000
	quit(0);
}
