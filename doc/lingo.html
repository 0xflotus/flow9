<link rel="stylesheet" type="text/css" media="screen" href="style.css?v1" />
<h1><em>lingo</em></h1>

<p><em>lingo</em> is a technology to make it easy to construct Domain Specific Languages or parsers. It is a language 
for specifying grammars that work well with <em>flow</em> code. It takes a grammar written in <em>lingo</em>, and can use that
to lex and parse a string and construct a structure to represent that. In other words, it is a language that
is useful to implement lexing, parsing and AST construction for compilers or interpreters. Lingo itself
is interpreted by a <em>flow</em> program, found in <code>lingo/pegcode/pegcompiler.flow</code> for the Lingo compiler that 
produces pegcode bytecode representing the grammar, and in <code>lingo/pegcode/pegcode.flow</code> for the parser that 
interprets pegcode bytecode.</p>

<p><em>lingo</em> is based on <a href="http://en.wikipedia.org/wiki/Parsing_expression_grammar">Parsing Expression Grammars (PEG)</a>
and inspired by <a href="http://tinlizzie.org/ometa/">Ometa</a>.</p>

<h2>Syntax</h2>

<p>A <em>lingo</em> grammar consists of productions:</p>

<pre><code>language = "World, hello" | "World, goodbye";
</code></pre>

<p>A production starts with a name ('language'), an equal sign, and then a list of choices separted by
pipes, and end with a semi-colon. This grammar matches the two strings "World, hello" or "World, goodbye".</p>

<p>Each choice itself is a sequence of parsing elements. The following matches the same grammar:</p>

<pre><code>language = "World," " " "hello" | "World," " " "goodbye";
</code></pre>

<p>where each choice is a sequence of three terminals. </p>

<p>A terminal is the name used for lexical elements in a grammar - i.e. the basic elementary symbols 
or words of the language, such as integers, keywords, identifiers, operator-symbols and similar. 
Conversely, non-terminals are the "grammar" rules, where the rules for how the terminals (words) 
can combine. In addition to terminals and non-terminals, normally white-space is considered a seperate
category. White-space is commonly any sequence, potentially empty, of space, tabs, newlines, carriage
returns as well as comments. White-space is parsed, but otherwise thrown away.</p>

<p>As you can see, in <em>lingo</em>, lexing and parsing is combined, so when you write <em>lingo</em>
grammars, you have to remember to specify where white-space is accepted. In other words, <em>lingo</em>
parses both terminal and non-terminals. In other parser-generators, such as Lex/Yacc, normally
you use Lex (flex) to <em>lex</em> the strings into terminals, and then Yacc defines the rules for how
to parse the stream of terminals. In <em>lingo</em>, these two concerns are handled in the same language,
making it quicker to use, easier to understand and more flexible.</p>

<p>You can reference other productions by name:</p>

<pre><code>language = world ws "hello" | world ws "goodbye";
world = "World,";
ws = " ";
</code></pre>

<p>Even though this grammar requires look-ahead to resolve, PEG grammars can parse such 
grammars efficiently, because it can cache partial parsings, and thus backtracking is
efficient.</p>

<p>The choice operator (<code>|</code>) is a committed choice operator, so if two choices are ambigious, 
the first one matching is chosen:</p>

<pre><code>dangling_else_solved = "if" exp "then" exp "else" exp | "if" exp "then" exp;
</code></pre>

<p>This grammar solves the classical <em>dangling else</em> ambiguity, and makes the <code>else</code>-clause
bind to the innermost <code>if</code>.</p>

<p>Besides the sequence of terminals and rules, you can use different operators:</p>

<pre><code>ws = " "+;                               // One or more spaces
star = "Hello" ws "cruel"* ws "world";   // Zero, one or more "cruel"s allowed
optional = "Bye" ws "cruel"? ws "world"; // Zero or one "cruel" allowed
letter = "a"-"z";                        // Character range, inclusive in both ends
letter_except_b = !'b' 'a'-'z';          // Negation - does NOT match if the given expression matches
</code></pre>

<p>Nested choices are also supported (except with the obsolete interpreter):</p>

<pre><code>does_not_work = "Hello" (" " | "\n")+ "world";
</code></pre>

<p>There are a few legacy places that use the obsolete interpreter. In those cases, move the nested 
choices to the top-most choice level and it will work fine:</p>

<pre><code>works = "Hello"$h sp+ "world" {$h};
sp = " " | "\n";
</code></pre>

<p>For all normal use, the compiler does this automatically in the background.</p>

<h2>Bindings and actions</h2>

<p>To build a structure to represent the outcome of a parsing, you can bind the results of productions
to names, and use actions to build structures. Let's start with a simple grammar for adding 
numbers:</p>

<pre><code>exp = int "+" exp 
    | int;
int = digit+;
digit = '0'-'9';
</code></pre>

<p>This grammar is not really useful, since it does not support white-space or other operations, but
it serves the purpose of explaining bindings.</p>

<p>We add bindings using ":" and "$" followed by a name, and then actions in <code>{}</code> to construct the results:</p>

<pre><code>exp = int:e1 "+" exp:e2 { Add(:e1, :e2) } 
    | int:e { :e };
int = digit+$d { Int(s2i($d)) };
digit = '0'-'9';
</code></pre>

<p>The <code>$name</code> syntax binds to the text that matches, while <code>:name</code> binds to the result of that production. 
This grammar will produce a structure like </p>

<pre><code>Add(Int(1), Add(Int(2), Int(3)))
</code></pre>

<p>for a string like "1+2+3", where <code>Add</code> and <code>Int</code> are flow structures defined like this in a flow file:</p>

<pre><code>Exp ::= Add, Int;
    Add : (e1 : Exp, e2 : Exp);
    Int : (i : int);
</code></pre>

<p>The <code>s2i</code> name is a predefined action that converts the string to a number. You can see which predefined actions
exist in <code>lingo/pegaction.flow</code>. It is relatively easy to add your own by adding to that default action tree.</p>

<p>Probably you've noticed that this grammar produces a wrong AST: it uses right recursion and as a result
it builds the tree starting from the rightmost operation. So instead of (1+2)+3 we get 1+(2+3). This doesn't
matter much if you use only '+' in your grammar. But it will matter when you use '-': 1-2-3 should be parsed as
(1-2)-3 rather than 1-(2-3). The easiest way to fix this issue is to use semantic actions and it will be explained
a bit further.</p>

<p>Note, a grammar must return a top-level construction. If you fail to do so the parsing will return "PARSING 
FAILED".</p>

<p>When you match varying number of items with ?, + or *, the semantic result is an array. With this in a flow
file:</p>

<pre><code>Exps(exps : [Exp]);
</code></pre>

<p>these semantic actions show how to capture zero, one or more expressions:</p>

<pre><code>optional = exp?:e { Exps(:e) };
plus = exp+:e { Exps(:e) };
star = exp*:e { Exps(:e) };
</code></pre>

<p>Unfortunately, you cannot include information about the AST data structures type in the lingo file so you have 
to either include the corresponding file in the flow program or, if you running lingo from command line, into 
<code>pegcompiler.flow</code>.</p>

<h2>Position of match in string</h2>

<p>As a special action, you can use <code>#</code> as a name which will give the index into the parsed string at this point.
The grammar</p>

<pre><code>a = 'a'+ {#}:s 'b'+ { BRange(:s, #) };
</code></pre>

<p>will give the position of where the b's start, as well as where they end.</p>

<h2>Testing a grammar</h2>

<p>In this section we are going to change files in flow/lib folder. Be sure not to commit this to the public repo.</p>

<p>The recommended way of testing a lingo grammar:</p>

<ul>
<li>put the lingo grammar in a separate file, with a .lingo extension;</li>
<li>put the AST into a separate flow file in flow/lib folder;</li>
<li>make a test file with a text to be parsed;</li>
<li>import the flow file with AST in <code>lib/lingo/pegcode/pegcompiler.flow</code> file;</li>
<li>run pegcompiler.flow passing the lingo and test files into it.</li>
</ul>

<p>Make a <code>sandbox/mygrammar.lingo</code> file with the contents:</p>

<pre><code>exp = int:e1 "+" exp:e2 { Add(:e1, :e2) }
    | int:e { :e };
int = digit+$d { Int(s2i($d)) };
digit = '0'-'9';
</code></pre>

<p>Make a <code>flow/lib/mygrammar.flow</code> file with the contents:</p>

<pre><code>Exp ::= Add, Int;
    Add : (e1 : Exp, e2 : Exp);
    Int : (i : int);
</code></pre>

<p>Make a <code>sandbox/testcase.txt</code> file with a text to be parsed like this:</p>

<pre><code>    11+15
</code></pre>

<p>There are no spaces or new lines in this sample.</p>

<p>Add this line to the top of <code>lingo/pegcode/pegcompiler.flow</code>:</p>

<pre><code>import mygrammar;
</code></pre>

<p>Run a command:</p>

<pre><code>flowcpp lingo/pegcode/pegcompiler.flow -- file=sandbox/mygrammar.lingo testfile=sandbox/testcase.txt
</code></pre>

<p>This will parse the grammar, check it for errors, and then use it to parse the contents of the <code>testcase.txt</code>
file. It will report whether the file parsed or not. If you want to inspect the semantic output of the file,
then add <code>result=1</code>:</p>

<pre><code>flowcpp lingo/pegcode/pegcompiler.flow -- file=sandbox/mygrammar.lingo testfile=sandbox/testcase.txt result=1
</code></pre>

<p>The output for the files above would be:</p>

<pre><code>Running grammar on sandbox/testcase.txt
PARSE OF TEST SUCCESFUL
Time to parse: 0.00010302734375
Result:
ParseResult(5, Some(Add(Int(11), Int(15))))
</code></pre>

<p>If we want to test the flow grammar itself, add this line to the top of <code>pegcompiler.flow</code>:</p>

<pre><code>import lingo/flow/flowast;
</code></pre>

<p>to get all definitions of the flow AST structures defined, and test with this</p>

<pre><code>flowcpp lingo/pegcode/pegcompiler.flow -- file=lib/lingo/flow.lingo testfile=lib/maybe.flow result=1
</code></pre>

<p>and it should dump the AST for the maybe.flow file.</p>

<p>A useful shorthand is to use <code>test=a=1</code> instead of <code>testfile</code> if you just need to parse a short string:</p>

<pre><code>flowcpp lingo/pegcode/pegcompiler.flow -- file=lib/lingo/flow.lingo test=a=1 result=1
</code></pre>

<p>which prints</p>

<pre><code>Running grammar on 'a=1'
Time to parse: 0
Result:
ParseResult(3, Some(FaProgram([], [FaVarDecl("a", [], [FaInt(1, 2, 3)], 0, 3)], 0)))
</code></pre>

<p>As you can see, the program also prints the number of seconds required to parse the string. Often, this number
is too small to be noticable. To get more statistical data, you can parse the same string many times using
the <code>testruns=10</code> flag:</p>

<pre><code>flowcpp lingo/pegcode/pegcompiler.flow -- file=lib/lingo/flow.lingo test=a=1 testruns=100
</code></pre>

<p>which takes 0.0156 seconds for 100 runs on my machine.</p>

<h2>Debug</h2>

<p>To help debugging lingo syntax and contructions the debug() pegaction, which prints the current Flow construct 
to console, helps quite a bit. Changing the example above: </p>

<pre><code>exp = int:e1 { debug(:e1) } "+" exp:e2 { debug(Add(:e1, :e2)) } 
    | int:e { :e };
int = digit+$d { debug(Int(s2i($d))) };
digit = '0'-'9';
</code></pre>

<p>will produce this console output</p>

<pre><code>Debug
[Int(1)]    // from the end of the int production
Debug
[Int(1)]    // from the start of the exp production
Debug
[Int(2)]    // from the end of the int production
Debug
[Add(Int(1), Int(2))]   // from the end of the exp production
</code></pre>

<p>for the input string "1+2".</p>

<h2>Tracing</h2>

<p>Another option to using the <code>debug()</code> action is to use tracing. This is done by adding <code>trace=1</code> to the invocation
of pegcompiler when testing your grammar:</p>

<pre><code>flowcpp lingo/pegcode/pegcompiler.flow -- file=lib/lingo/flow.lingo test=a=1 trace=1
</code></pre>

<p>The result is something like this:</p>

<pre><code>Running grammar on 'a=1'
Debug: ["Unknown var: ws", ""]
Debug: ["Unknown var: letter", "a"]
Debug: ["Unknown var: id", "a"]
Debug: ["Unknown var: ws", ""]
Debug: ["Unknown var: ws", ""]
Debug: ["Unknown var: digit", "1"]
Debug: ["Unknown var: int", "1"]
Debug: ["Unknown var: number", FaInt(1, 2, 3)]
Debug: ["Unknown var: ws", ""]
Debug: ["Unknown var: atom", FaInt(1, 2, 3)]
Debug: ["Unknown var: call", "1"]
Debug: ["Unknown var: factor", "1"]
Debug: ["Unknown var: term", "1"]
Debug: ["Unknown var: exp6", "1"]
Debug: ["Unknown var: exp5", "1"]
Debug: ["Unknown var: exp4", "1"]
Debug: ["Unknown var: exp3", "1"]
Debug: ["Unknown var: exp2", "1"]
Debug: ["Unknown var: exp", "1"]
Debug: ["Unknown var: lastexp", "1"]
Debug: ["Unknown var: assign", FaVarDecl("a", [], ["1"], 0, 3)]
Debug: ["Unknown var: toplevelDeclaration", "a=1"]
Debug: ["Unknown var: program", FaProgram([], ["a=1"], 0)]
PARSE OF TEST SUCCESSFUL
Time to parse: 0.0156240234375
</code></pre>

<p>For each succesful matched rule, the name of the rule is printed, with the result of the rule after that.
Thus, the first line shows the <code>ws</code> rule matched the empty string. The next shows that <code>letter</code> matched 
'a'. The third line shows that the "id" rule gave the semantic result "a", and so on.</p>

<p>If we just wanted to focus on a few rules, we could list the names of the rules we are interested in:</p>

<pre><code>flowcpp lingo/pegcode/pegcompiler.flow -- file=lib/lingo/flow.lingo test=a=1 trace=atom,exp
</code></pre>

<p>which results in this:</p>

<pre><code>Running grammar on 'a=1'
Debug: ["Unknown var: atom", FaInt(1, 2, 3)]
Debug: ["Unknown var: exp", FaInt(1, 2, 3)]
PARSE OF TEST SUCCESFUL
Time to parse: 0
</code></pre>

<h2>Caching</h2>

<p>To speed up parsing, it is possible to enable caching of the parsing of productions. This is useful
to make backtracking efficient. To enable caching, append an exclamation mark right after the production name:</p>

<pre><code>digit! = '0'-'9';
</code></pre>

<p>To find out what parts of the grammar should have caching enabled, there is some cache-hit analysis
code in <code>pegcode.flow</code> that should be uncommented. Search for 'Enable cache analysis here:' in that file.</p>

<p>After that, change the <code>grammar2code.flow</code> code where it says</p>

<pre><code>// Enable cache analysis here:
if (/* true || */ g.productions[i].caching) {
</code></pre>

<p>to</p>

<pre><code>// Enable cache analysis here:
if (true || g.productions[i].caching) {
</code></pre>

<p>to force caching for all productions. Then run your parser on some representative input using pegcompiler
above, and it will report a list of productions that might benefit from caching.</p>

<p>Add <code>!</code> to those productions, and uncomment the forced addition of caching for all productions in <code>grammar2code.flow</code> 
to verify you did it right. Then get rid of the cache profiling, and be happy. When optimizing, it is always
best to have real timings before and after the optimization on representative inputs to verify that
you are in fact optimizing.</p>

<p>If you are not using the pegcompiler to test, you can also manually call something
like</p>

<pre><code>grammar = compilePegGrammar2("#include lingo/flow.lingo");
dumpParseCacheStats(foldTree(grammar.second, [], \k, v, acc -&gt; arrayPush(acc, v)))
</code></pre>

<p>to dump the cache stats in your own program.</p>

<h2>Clearing the cache</h2>

<p>As an advanced feature, Lingo supports marking places in the grammar when the parsing cache can be safely
cleared using the ~ operator:</p>

<pre><code>program = exp ("," ws ~exp)*;
</code></pre>

<p>In this example, we know that if an expression parsed, there is never a need to backtrack, and thus it
is safe to clear the parsing cache. That is what the ~ operator does. See papers by Mizushima and friends
to learn more: <a href="http://www.romanredz.se/papers/FI2015.pdf">Cut points in PEG</a>. We do not have any
automated analysis to find cutpoints, but we do support up-cuts and down-cuts with the semantics of
clearing the cache. There is no choice-cut optimization implemented for these operators yet, but since
we optimize the pegcode, there is probably little gain to be had from this.</p>

<h2>Instruction level rule profiling</h2>

<p>Normally, you get acceptable performance from adding the correct caching as noticed above, but sometimes,
you really need to optimize a grammar to the max. In that situation, it is useful to do a very detailed 
profiling of the grammar. This is possible by searching for </p>

<pre><code>// Enable rule profiling here
</code></pre>

<p>in <code>pegcode.flow</code> and change two places to enable rule profiling. Then test your grammar with pegcompiler 
as described above, and you will get a detailed analysis of what productions are used and how frequent, 
including how many times each individual pegcode instruction is run to parse your test case. This data allows 
you to see what choices to reorder to move the most common taken routes to the top, notice any parsing errors, 
and otherwise allow fine-tuning of the grammar.</p>

<h2>Common problems</h2>

<p>Unfortunately, our parser is not clever enough to find certain, common problems. Grammars
like the following will seemingly compile just fine, but when you try to use them, they will 
not work.</p>

<p>Unreachable choices:</p>

<pre><code>unreachable_choice = "a" | "a" "b";
</code></pre>

<p>Since the choice operator matches the first successful choice, the second choice is never
matched.</p>

<p>Direct left recursion will not work:</p>

<pre><code>string_of_a = string_of_a 'a' | 'a';
</code></pre>

<p>Most often, that can be expressed more clearly with a plus operator anyways:</p>

<pre><code>string_of_a = 'a'+;
</code></pre>

<p>Indirect left recursion:</p>

<pre><code>exp = product | sum | int;
sum = exp '+' exp | exp '-' exp;
product = exp '*' exp | exp '/' exp;
int = digit+;
digit = '0'-'9';
</code></pre>

<p>This will not parse. You have to restructure the grammar not to have any indirect left-recursion:</p>

<pre><code>exp = product '+' exp | product '-' exp | product;
product = int '*' product | int '/' product | int;
int = digit+;
digit = '0'-'9';
</code></pre>

<h2>Basic way of using a grammar in a flow program</h2>

<p>Normally using the pegcompiler directly to compile and test your grammar is the best way to develop it,
but sometimes, you want to use special semantic actions or integrate the grammar in an interactive
program. In this case, <em>flow</em> code like the following will work to prepare and run the grammar:</p>

<pre><code>import lingo/pegcode/driver;

// Singleton keeping the compiled grammar
lingoGrammarOp : ref Maybe&lt;[PegOp]&gt; = ref None();

lingoGrammar() {
    // Prepare the grammar in the given file using flow syntax for inlining a string from a file
    // but be sure to only do this once
    onlyOnce(lingoGrammarOp, \ -&gt; {
        compilePegGrammar("#include lingo/lingo.lingo");
    });
}

main() {
    // Here we parse a string against the grammar
    result = parsic(lingoGrammar(), "Hello world", defaultPegActions);
}
</code></pre>

<p><code>result</code> will contain the result of the action of the first production in the grammar. </p>

<p>In production code where less console output is wanted <code>parsic3</code> can be used. It prepares a nice
error message on parse failures, as well as a default value on errors.</p>

<p>Once the grammar works, then you want to precompile the grammar for effiency and use that in production. 
See the next section to learn how to do that.</p>

<h2>Using custom semantic actions</h2>

<p>Now let's consider the example with a sum of integers and see how we can build a correct tree. Let's use
subtraction to see why the order really matters. Here is the grammar:</p>

<pre><code>exp = int:e1 "-" exp:e2 { Sub(:e1, :e2) } 
    | int:e { :e };
int = digit+$d { Int(s2i($d)) };
digit = '0'-'9';
</code></pre>

<p>And flow structures:</p>

<pre><code>Expr ::= Int, Sub;
Int(value : int);
Sub(l : Expr, r : Expr);
</code></pre>

<p>It uses right-recursion and folds the expression starting with the rightmost operation. Let's review parsing
step by step for string "1-2-3". We expect to get <code>exp</code> in the input. The first rule starts with an <code>int</code>, so we
grab all digits ("1"), convert them to Int(1) and put it on stack:</p>

<pre><code>1-2-3
 ^
Stack: Int(1)
</code></pre>

<p>The next expected terminal is "-" and we find it in the input string, so we continue with the first rule:</p>

<pre><code>1-2-3
  ^
Stack: Int(1)
</code></pre>

<p>Repeat the same once again, so we get "1-2-" parsed and have Int(1) and Int(2) on stack:</p>

<pre><code>1-2-3
    ^
Stack: Int(1), Int(2)
</code></pre>

<p>We try to parse the rest of the string (which is "3") by matching to the first rule of <code>exp</code>. We successfully
parse "3", put Int(3) on stack, but fail to find '-', so we forget all work in the current step and switch to
the second rule of <code>exp</code>.</p>

<pre><code>1-2-3
    ^
Stack: Int(1), Int(2)
</code></pre>

<p>In second rule we again parse "3" and put Int(3) on stack</p>

<pre><code>1-2-3
     ^
Stack: Int(1), Int(2), Int(3)
</code></pre>

<p>We reach the end of input and do not expect anything else, so we fold our results:</p>

<pre><code>AST: Empty
Stack: Int(1), Int(2), Int(3)
</code></pre>

<p>We apply the second rule of <code>exp</code>:</p>

<pre><code>AST: Int(3)
Stack: Int(1), Int(2)
</code></pre>

<p>Then the first rule:</p>

<pre><code>AST: Sub(Int(2), Int(3))
Stack: Int(1)
</code></pre>

<p>And once again:</p>

<pre><code>AST: Sub(Int(1), Sub(Int(2), Int(3)))
Stack:
</code></pre>

<p>So we get Sub(Int(1), Sub(Int(2), Int(3))) which corresponds to 1-(2-3).</p>

<p>Instead of parsing each term separately we can grab all of them at once and then build the tree in the correct
order using a special semantic action <code>buildSub</code> (we'll define it later):</p>

<pre><code>exp = int:i sub*:is {buildSub(:i, :is)};
sub = '-' int:t { :t };
int = digit-$d { Int(s2i($d)) };
digit = '0'-'9';
</code></pre>

<p>Let's see how it will parse the same input. The first integer is parsed in the same way:</p>

<pre><code>1-2-3
 ^
Stack: Int(1)
</code></pre>

<p>Now we expect any number of <code>sub</code>. The parser is greedy so it will take as much of them as it can. Please
notice that we use <code>sub*</code>, not just <code>sub</code>, so we get an array of <code>Int</code>s.</p>

<pre><code>1-2-3
   ^
Stack: Int(1), [Int(2)]

1-2-3
     ^
Stack: Int(1), [Int(2), Int(3)]
</code></pre>

<p>We reach the end of input, but we do not expect anything else. So we fold the results and get 
buildSub([Int(1), [Int(2), Int(3)]])</p>

<p>Now we need to define <code>buildSub</code> in our code. We have a list of arguments: the first one is the leftmost
integer (which corrseponds to <code>int</code> in <code>exp</code> rule), the second one is the array of all other integers
(which corresponds to <code>sub*</code> in the <code>exp</code> rule), so we can just fold them this way:</p>

<pre><code>buildSub(xs : [flow]) {
    fold(xs[1], xs[0], \x, acc -&gt; Sub(x, acc))
}
</code></pre>

<p>This function will produce Sub(Sub(Int(1), Int(2)), Int(3)) for the given example which can be evaluated as
(1-2)-3. Now all we need is to pass this semantic action to the parser:</p>

<pre><code>specialPegActions = {
    t = setTree(defaultPegActions.t, "buildSub", buildSub);
    SemanticActions(t);
}
parsic(lingoGrammar(), s, specialPegActions);
</code></pre>

<h2>Precompiling a grammar for efficiency</h2>

<p>Instead of compiling the grammar at runtime with code like the above, it is more efficient to precompile 
the grammar to pegcodes and then just use the opcodes at runtime.</p>

<p>This is done like this:</p>

<pre><code>flowcpp lingo/pegcode/pegcompiler.flow -- file=path/to/grammar.lingo out=path/to/grammar_pegop.flow
</code></pre>

<p>which will produce a <code>grammar_pegop.flow</code> file which defines a global variable named after the file. In this instance, 
<code>pegop4Grammar</code>. In general, the name of the variable will be <code>pegop4&lt;<em>name-of-grammar-file</em>></code>. 
On Windows be careful to use forward dashes in the path in the file parameter. The variable will contain 
the compiled grammar. To use it, also construct a driver using a <code>flowfile=path/to/grammar_parse.flow</code> parameter,
and you will get a suitable .flow file that defines a parsing function for you.</p>

<p>You can also set the result-type of the grammar using <code>parsetype=MyAst</code> switch to make the parsing function
more typesafe. An example driver as produced could look like this:</p>

<pre><code>// Generated by
//   flowcpp lingo/pegcode/pegcompiler.flow -- file=lib/formats/mouse/mouse.lingo out=lib/formats/mouse/mouse_pegop.flow flowfile=lib/formats/mouse/mouse_parse.flow parsetype=MGrammar

import lingo/pegcode/driver;
import lib/formats/mouse/mouse_pegop;
import lib/formats/mouse/mouse_ast;

export {
    // Parses a string in Mouse format. Returns 'def' on failure.
    // The second int is how far we parsed, and the string is an error message on failure.
    parseMouse(text : string, def : MGrammar) -&gt; Triple&lt;MGrammar, int, string&gt;;
}

parseMouse(text : string, def : MGrammar) -&gt; Triple&lt;MGrammar, int, string&gt; {
    parsic3(pegOps4Mouse, text, defaultPegActions, def);
}
</code></pre>

<p>The command line listed in the comment thus produced both the <code>mouse_pegop.flow</code> and <code>mouse_parser</code> file, 
and by convention included a <code>mouse_ast</code> import to get the AST struct node definitions.</p>

<p>This way, the grammar is compiled into optimized pegcodes, and the startup is much faster, since
there is no parsing of the grammar required. This is the best way to use Lingo in production.</p>

<p>If you do this, obviously, you have to regenerate the <code>*_pegop.flow</code>  file when the grammar changes, just like 
when you use Yacc or Bison.</p>

<p>If you need to have debugging info for the grammar, you can add <code>debug=1</code> to the pegcompiler, and it will also
define a pegop4GrammarDebug variable. This is rarely needed.</p>

<h2>How to handle white-space</h2>

<p>You must pick a consistent policy for white-space (ws), so you do not have 
more than one white-space rule in a row (for efficiency), but more importantly: 
so that you can be sure there is no string that will be unparseable because it
has a whitespace too much. The latter is really hard to debug.</p>

<p>The best policy is to add "ws" at the start of the top-level grammar, and
then after each token or terminal rule. Then you have the invariant that
every non-terminal rule supports and parses trailing ws.</p>

<p>As an example, here is a grammar for very simple expressions with integers, 
ids &amp; addition:</p>

<pre><code>// The very first rule should have ws at the start to take leading ws
program = ws exp;

// There are rules that all directly or indirect eat trailing white-space
// so we only have to handle ws after the token "+". exp1 and exp 
// recursively ensure that all "exp" handle trailing ws.
exp = exp1 "+" ws exp
    | exp1;

// When we reference token rules like int and id, we have to handle
// following ws our selves
exp1 = int ws | id ws;

// These are all tokens, and do not eat whitespace.
// When referencing these, you have to add "ws" at the end to handle
// ws after these rules.
int = digit+;
id = letter+;
digit = '0'-'9';
letter = 'a' - 'z' | 'A' - 'Z';

// The rules for white-space
ws = s*;
s = " " | "\t" | "\n";
</code></pre>

<p>The key insight is to know for each rule whether it eats trailing whitespace 
or not. Strive to make your rules always eat whitespace, and then you only
need to add whitespace after strings, i.e. tokens, also knows as non-terminals.</p>

<p>Conventionally, the token-productions for terminals are placed at the end of the
grammar, and typically have names such as id, int, and similar.</p>

<p>As you can see, we naturally divide the grammar into token-like productions and 
normal rules. Then normal rules always eat white-space directly or indirectly, 
while the token-like never eat ws.</p>

<p>This pattern will parse correctly, is easy to follow and will be efficient in that 
white-space is always only parsed once.</p>

<p>A common mistake is to add too many "ws" references:</p>

<pre><code>program = ws exp ws;

exp = "break" ws;
</code></pre>

<p>In this example, it is wrong to have a "ws" after the "exp" reference in the 
"program" rule. This is wrong, since "exp" handles "ws" itself, so there is
no need for references to this rule to also do it.</p>

<p>The reason is that it causes unnecessary unambiguity, since effectively, the result 
corresponds to a rule like this:</p>

<pre><code>program = ws "break" ws ws;
</code></pre>

<p>Notice the double "ws" at the end. If we expand "ws" itself, we get:</p>

<pre><code>program = s* "break" s* s*;
</code></pre>

<p>Now, it is clear that there are many ways to parse multiple spaces: If we have 
100 spaces after the "break" keyword, there would be 100 different ways
to parse that white-space depending on how many the last s* handles. Normally, 
this does not happen, since <em>Lingo</em> is eager and deterministic, but in case of 
parse errors, the engine will backtrack and could run into exponential slowdowns 
because of an error like this.</p>

<h2>Checking text for matching to grammar</h2>

<p>If you just want to check if some text is matching to grammar you can use 
<code>matchLingo(grammar : string, text : string) -&gt; bool</code> from <code>lingo/match.flow</code>.</p>

<pre><code>matchLingo("#include my.lingo", "r5")
matchLingo("digit = ('0'-'9')$s {$s};", "1")
</code></pre>

<h2>Joining multiple grammars</h2>

<p>When you use the pegcompiler to compile Lingo grammars, it is possible to provide multiple <code>.lingo</code> files to 
join together into one grammar.</p>

<p>Consider this example:</p>

<p>In file a.lingo, we have:</p>

<pre><code>number = int;
int = digit+;
digit = '0'-'9';
</code></pre>

<p>In file b.lingo, we have:</p>

<pre><code>number = double | number;
double = int "." int;
</code></pre>

<p>Then we can combine these grammars into one using</p>

<pre><code>flowcpp lingo/pegcode/pegcompiler.flow -- file=a.lingo,b.lingo out=combined_pegop.flow
</code></pre>

<p>and it would be the same as if I had this grammar:</p>

<pre><code>number = double | number;
double = int "." int;
int = digit+;
digit = '0'-'9';
</code></pre>

<p>in one file. This is useful when you want to make hybrid languages. As an example, see Datawarp, which combines
SQL and Flow syntax.</p>

<p>In terms of the corresponding mixed AST that will result, a useful practice is to introduce a "Foreign" AST node
that allows foreign grammars to introduce their own constructs into the AST:</p>

<pre><code>Ast ::= Add, Divide, Integer, Foreign;
Foreign(payload : ?, children : [Ast]);
</code></pre>

<p>See the flow AST for an example.</p>

<h2>Compiling to native flow code</h2>

<p>When the ultimate parsing speed is required, you can compile a Lingo grammar to a flow program, 
which parses the grammar efficiently.</p>

<pre><code>flowcpp lingo/pegcode/pegcompiler.flow -- file=lib/lingo/flow.lingo flowparser=path/flow_parser.flow
</code></pre>

<p>This will produce a parser for the flow grammar in the <code>flow_parser.flow</code> file. That file will
expose exactly one function:</p>

<pre><code>export {
    parse_flow(t : string) -&gt; SyntaxTree;
}
</code></pre>

<p>This function takes a string, parses it, and returns a <code>SyntaxTree</code>. <code>SyntaxTree</code> is defined like this:</p>

<pre><code>SyntaxTree(rule : string, choice : int, start : int, end : int, children : [[SyntaxTree]]);
</code></pre>

<p>As you can see, for each rule, we will record what rule was matched, what choice in that rule was used,
what was the starting and ending characters positions for the match, as well as the results of any
recursive rules matched in our rule.</p>

<p>This compiler will thus ignore all bindings and actions of your grammar, and always produce
a SyntaxTree-based AST.</p>

<p>If there is a compile error, the "choice" field will be -1, and the "rule" string will contain a
error message. You can use the <code>start</code> and <code>end</code> positions to find out where the parse error is.</p>

<p>To make it easier to work with the SyntaxTree structure, you can use the <code>flowparserast=flow_ast.flow</code>
switch:</p>

<pre><code>flowcpp lingo/pegcode/pegcompiler.flow -- file=lib/lingo/flow.lingo flowparser=path/flow_parser.flow
            flowparserast=flow_ast.flow
</code></pre>

<p>and now the compiler will also produce a file with a long list of functions, which help extract the
results of each match. </p>

<p>To extract the matching text of a SyntaxTree node, you can use <code>grabSTText</code> from 
<code>lingo/compiler/syntaxtree_util</code>. You give it a SyntaxTree node, the original text that was
passed to <code>parse_flow</code>, and you get the matching text out.</p>

<p>The parsers produced by this approach are up to 10 times faster than the pegcode-based compilers,
but the downside is that the AST is untyped, and you have to manually convert that to a suitable
typed AST using the AST functions and <code>grabSTText</code>.</p>

<p>Before you use this approach, be sure to profile and optimize your grammar using the normal 
pegcode-based parser and the instructions above. All optimizations done with that will carry
directly over to the flow-based parser.</p>
