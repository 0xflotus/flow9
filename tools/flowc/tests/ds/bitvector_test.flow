import ds/bitvector;
import arrayutils;
import stringmath;


createRandomLength(maxLen : int) -> int {
	cast(random() * cast(maxLen : int -> double) : double -> int)
}

createRandomBoolArray(len : int) -> [bool] {
	arrayRepeatDeferred(\ -> random() > 0.5, len);
}

dumpBitVector(bitset : BitVector) -> string {
	fold(
		enumFromTo(0, bitset.bitcount - 1), "",
		\acc, i -> {
			switch (getBitVector(bitset, i)) {
				Some(val): if (val) acc + "1" else acc + "0";
				None(): acc + "_";
			}
		}
	)
}

dumpBitArray(bitarr : [bool]) -> string {
	fold(
		enumFromTo(0, length(bitarr) - 1), "",
		\acc, i ->
			if (bitarr[i]) acc + "1" else acc + "0"
	)
}

testsOnCreation(maxLen : int) -> void {
	time_start = timestamp();
	iter(enumFromTo(1, 100), 
		\n -> {
			len = createRandomLength(maxLen);
			randArr = createRandomBoolArray(len);
			//println("RUNNING " + i2s(n) + " TEST OF SIZE: " + i2s(len));
			bitsetPos = foldi(randArr, makeBitVector(len, \__ -> false),
				\i, acc, bit -> if (bit) setBitVector(acc, i) else acc
			);
			bitsetNeg = foldi(randArr, makeBitVector(len, \__ -> true),
				\i, acc, bit -> if (bit) acc else unsetBitVector(acc, i)
			);
			
			error = \err, pos -> {
				println(err);
				println("bitarr:    " + dumpBitArray(randArr));
				if (pos) {
					println("bitset(P): " + dumpBitVector(bitsetPos));
				} else {
					println("bitset(N): " + dumpBitVector(bitsetNeg));
				}
				quit(0);
			}
			
			iteri(randArr, \i, bit -> {
					if (Some(bit) != getBitVector(bitsetPos, i)) {
						error("TEST FAIL: bits differ", true);
					}
					if (Some(bit) != getBitVector(bitsetNeg, i)) {
						error("TEST FAIL: bits differ", false);
					}
				}
			);
			switch (getBitVector(bitsetPos, -1)) {
				Some(__):  error("TEST FAIL: index '-1' has value", true);
				None(): { }
			}
			switch (getBitVector(bitsetPos, len)) {
				Some(__):  error("TEST FAIL: index '" + i2s(len) + "' has value", true);
				None(): { }
			}
			switch (getBitVector(bitsetNeg, -1)) {
				Some(__):  error("TEST FAIL: index '-1' has value", false);
				None(): { }
			}
			switch (getBitVector(bitsetNeg, len)) {
				Some(__):  error("TEST FAIL: index '" + i2s(len) + "' has value", false);
				None(): { }
			}
		}
	);
	time = (timestamp() - time_start) / 1000.0;
	println("TEST COMPLETED IN: " + d2st(time, 2) + "s");
}

testsOnOperations(maxLen : int) -> void {
	time_start = timestamp();
	iter(enumFromTo(1, 100), 
		\n -> {
			len = createRandomLength(maxLen);
			randArr1 = createRandomBoolArray(len);
			randArr2 = createRandomBoolArray(len);
			
			//println("RUNNING " + i2s(n) + " TEST OF SIZE: " + i2s(len));
			
			bitset1 = makeBitVector(len,
				\i -> {
					if (i < length(randArr1)) {
						randArr1[i]
					} else {
						println("index violation: " + i2s(i) + " >= " + i2s(length(randArr1)));
						false;
					}
				}
			);
			bitset2 = makeBitVector(len, 
				\i -> {
					if (i < length(randArr2)) {
						randArr2[i]
					} else {
						println("index violation: " + i2s(i) + " >= " + i2s(length(randArr2)));
						false;
					}
				}
			);
			
			error = \err -> {
				println(err);
				quit(0);
			}
			
			switch (bitVectorOr(bitset1, bitset2)) {
				Some(bitsetUnion): {
					iteri(randArr1, 
						\i, bit -> {
							if (Some(bit || randArr2[i]) != getBitVector(bitsetUnion, i)) {
								error("TEST FAIL: union differs");
							}
						}
					)
				}
				None(): error("union of bitsets is not calculated");
			}
			switch (bitVectorAnd(bitset1, bitset2)) {
				Some(bitsetIntersect): {
					iteri(randArr1, 
						\i, bit -> {
							if (Some(bit && randArr2[i]) != getBitVector(bitsetIntersect, i)) {
								error("TEST FAIL: intersection differs");
							}
						}
					)
				}
				None(): error("intersection of bitsets is not calculated");
			}
			bitsetComplement = bitVectorNot(bitset1);
			iteri(randArr1, 
				\i, bit -> {
					if (Some(!bit) != getBitVector(bitsetComplement, i)) {
						error("TEST FAIL: complemetation differs");
					}
				}
			);
		}
	);
	time = (timestamp() - time_start) / 1000.0;
	println("TEST COMPLETED IN: " + d2st(time, 2) + "s");
}

main() -> int {
	maxLen = 20000;
	println("\nTESTS OF CREATION OF IMMUTABLE BITSETS");
	testsOnCreation(maxLen);

	println("\nTESTS OF SET-THEORETIC IMMUTABLE OPERATIONS");
	testsOnOperations(maxLen);

	println("=========================");
	println("ALL BIT VECTOR TESTS PASSED");
	0
}
