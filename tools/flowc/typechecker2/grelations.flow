import tools/flowc/typechecker2/gtype;
import tools/flowc/typechecker2/gtype_util;
import ds/treeutils;

export {
	// This exposes the unifications we need to consider
	makeGRelations() -> GRelations;

	addGRelation(r : GRelations, lower : GType, upper : GType, pos : int) -> GRelations;

	getGUpperRelations(r : GRelations, from : GType) -> Set<GType>;
	getGLowerRelations(r : GRelations, from : GType) -> Set<GType>;
	getGRelationPositions(r : GRelations, from : GType) -> Set<int>;

	getGUpperTransitiveRelations(r : GRelations, from : GType) -> Set<GType>;
	getGLowerTransitiveRelations(r : GRelations, from : GType) -> Set<GType>;

	transitiveGUpperRelations(r : GRelations, from : GType, seen : Set<GType>) ->  Set<GType>;
	transitiveGLowerRelations(r : GRelations, from : GType, seen : Set<GType>) ->  Set<GType>;

	// Add .dot output to help debug these graphs
	saveGDot(env : GEnv, name : string) -> void;
}

makeGRelations() -> GRelations {
	GRelations(makeTree(), makeTree(), makeTree(), makeTree(), makeTree());
}
 
addGRelation(r : GRelations, from : GType, to : GType, pos : int) -> GRelations {
	if (containsMultimap(r.uppers, from, to)) r
	else {
		// Also calculate the transitive closure for the uppers
		nuppers = insertSet(
			mergeSets(
				lookupMultimap(r.uppersTransitive, to),
				lookupMultimap(r.uppersTransitive, from),
			), to
		);

		// TODO: We also have to update all our lower friends transitively, since they
		// are now also connected to all of this

		// Also calculate the transitive closure for the lowers
		luppers = insertSet(
			mergeSets(
				lookupMultimap(r.lowersTransitive, from),
				lookupMultimap(r.lowersTransitive, to),
			), from
		);

		// TODO: We also have to update all our upper friends transitively, since they
		// are now also connected to all of this

		GRelations(
			insertMultimap(r.uppers, from, to),
			insertMultimap(r.lowers, to, from),
			setTree(r.uppersTransitive, from, nuppers),
			setTree(r.lowersTransitive, to, luppers),
			insertMultimap(
				insertMultimap(r.positions, to, pos),
				from, pos
			),
		);
	}
}

getGUpperRelations(r : GRelations, from : GType) -> Set<GType> {
	lookupMultimap(r.uppers, from);
}

getGLowerRelations(r : GRelations, from : GType) -> Set<GType> {
	lookupMultimap(r.lowers, from);
}

getGUpperTransitiveRelations(r : GRelations, from : GType) -> Set<GType> {
	lookupMultimap(r.uppersTransitive, from);
}

getGLowerTransitiveRelations(r : GRelations, from : GType) -> Set<GType> {
	lookupMultimap(r.lowersTransitive, from);
}

getGRelationPositions(r : GRelations, from : GType) -> Set<int> {
	lookupMultimap(r.positions, from);
}

transitiveGUpperRelations(r : GRelations, from : GType, acc : Set<GType>) -> Set<GType> {
	if (containsSet(acc, from)) {
		acc
	} else {
		uppers = getGUpperRelations(r, from);
		foldSet(uppers, acc, \acc2, edge -> {
			nacc = insertSet(acc2, edge);
			transitiveGUpperRelations(r, edge, nacc)
		});
	}
}

transitiveGLowerRelations(r : GRelations, from : GType, acc : Set<GType>) -> Set<GType> {
	if (containsSet(acc, from)) {
		acc
	} else {
		uppers = getGLowerRelations(r, from);
		foldSet(uppers, acc, \acc2, edge -> {
			nacc = insertSet(acc2, edge);
			transitiveGLowerRelations(r, edge, nacc)
		});
	}
}

saveGDot(env : GEnv, name : string) -> void {
	dot = grelations2dot(env, env.grelations);
	setFileContent(name + ".dot", dot);

	{}
}

grelations2dot(env : GEnv, g : GRelations) -> string {
	type2node = \type -> {
		"\"" + gtype2string(env, type) + "\""
	}
	types = foldTree(g.uppers, makeSet(), \k, v, acc -> {
		insertSet(acc, k);
	});
	nodesDump = foldSet(types, "", \acc, type -> {
		text = type2node(type);
		acc + text + " [label=" + text + "];\n"
	});
	edgesDump0 = foldTree(g.uppers, "", \from, tos, acc -> {
		foldSet(tos, acc, \acc1, to -> {
			acc1 + type2node(from) + " -> " + type2node(to) + ";\n"
		});
	});

	edgesDump = if (true) edgesDump0 else foldTree(g.uppersTransitive, edgesDump0, \from, tos, acc -> {
		text = type2node(from);
		foldSet(tos, acc, \acc1, to -> {
			acc1 + type2node(from) + " -> " + type2node(to) + " [color=grey label=" + text +  "];\n"
		});
	});

	"digraph graphname {\n" +
		nodesDump + "\n" +
		edgesDump + "\n" +
	"}"
}
