import tools/flowc/typechecker2/gtype;
import tools/flowc/prettyprint;

export {
	gtype2string(env : GEnv, type : GType) -> string;
}

gtype2string(env : GEnv, type : GType) -> string {
	doGtype2string(env, type, makeSet());
}

doGtype2string(env : GEnv, type : GType, seen : Set<int>) -> string {
	pt = \t -> prettyFcType(FcPretty(false, false, ^(env.env.local.tyvars), ^(env.env.local.typars)), t, makeSet());
	switch (type) {
		GFlow(): "flow";
		GTypeVar(id): {
			alpha = "Î±" + i2s(id);
			if (containsSet(seen, id)) alpha
			else {
				switch (lookupTree(env.tyvars, id)) {
					None(): alpha;
					Some(rt): if (env.verbose > 1) alpha + " (=" + doGtype2string(env, rt, insertSet(seen, id)) + ")" else doGtype2string(env, rt, insertSet(seen, id));
				}
			};
		}
		GTypePar(id): id;
		GFunction(args, rt): {
			"(" + superglue(args, \a -> doGtype2string(env, a, seen), ", ") + ") -> " + doGtype2string(env, rt, seen);
		}
		GArray(at): {
			 "[" + doGtype2string(env, at, seen) + "]";
		}
		GRef(read): {
			 "ref " + doGtype2string(env, read, seen);
		}
		GBasicType(tt): pt(tt);
		GSubType(e): gsubtypeexpr2string(env, e, seen);
	}
}

gsubtypeexpr2string(env : GEnv, expr : GSubTypeExpr, seen : Set<int>) -> string {
	switch (expr) {
		GBounded(lower, upper): {
			// [l..y]
			"{" 
			+ gsubtypeexpr2string(env, lower, seen)
			+ " .. " 
			+ gsubtypeexpr2string(env, upper, seen)
			+ "}"
		}
		GNamed(name, tps): {
			name + "<" + superglue(tps, \a -> doGtype2string(env, a, seen), ", ") + ">";
		}
		GTypeVar(id): doGtype2string(env, expr, seen);
		GField(name, type): "(." + name + ":" + doGtype2string(env, type, seen) + ")";
		GAnd(exprs): "(" + superglue(exprs, \a -> gsubtypeexpr2string(env, a, seen), " & ") + ")";
		GOr(exprs): "(" + superglue(exprs, \a -> gsubtypeexpr2string(env, a, seen), " | ") + ")";
		GTopBottom(): "*";
	}
}
