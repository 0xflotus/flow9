import tools/flowc/incremental/fitype;
import tools/flowc/manipulation/split_expressions;
import tools/flowc/backends/common;
import tools/flowc/backends/wasm/wasm_types;
import tools/flowc/backends/wasm/f2wasm_utils;
import tools/flowc/backends/wasm/wasm_exp;
import tools/flowc/backends/wasm/wasm_exp_convert;
import ds/array;
import string;

export {
    fiWasmFindLastStatements(ex : WasmExp) -> Set<WasmExp>;
    fiWasmGenerateDestructors(vars : [Pair<string, WasmTypeDescriptor>]) -> string;
	fiWasmGenerateConstructor(type : WasmTypeDescriptor) -> Wasm2Cmd;
    fiWasmInjectTempVariables(ex : FiExp) -> FiExp;
    fiWasmInjectIncrements(e : WasmExp) -> WasmExp;
}

fiWasmFindLastStatements(ex : WasmExp) {
    ex0 : WasmExp = ex;
    switch (ex0 : WasmExp) {
		WasmLambda(__, __, __, __): makeSet1(ex);
		WasmCall(__, __, __, __, __): makeSet1(ex);
		WasmVar(__, __, __): makeSet1(ex);
		WasmLet(__, __, __, e2, __, __): 
			switch (e2) {
				WasmVoid(__): makeSet1(ex);
				default: fiWasmFindLastStatements(e2);
			} 
		WasmIf(e1, e2, e3, __, __): // consider both if and else branches
			mergeSets(fiWasmFindLastStatements(e2), fiWasmFindLastStatements(e3));
		WasmSwitch(__, __, cases, __, __): 
			mergeSetsA(map(cases, \c -> fiWasmFindLastStatements(c.body)));
		WasmCast(__, __, __, __, __): makeSet1(ex);
		WasmSeq(es, __, iid): // only consider last element of a sequence
			fiWasmFindLastStatements(lastElement(es, WasmVoid(iid)));
		// if there are calls inside es, CallPrim would be performed after it - 
		//   therefore, those calls aren't tail
		WasmCallPrim(__, __, __, __): makeSet1(ex); 
		WasmVoid(__): makeSet1(ex);
		WasmDouble(__, __): makeSet1(ex);
		WasmInt(__, __): makeSet1(ex);
		WasmString(__, __): makeSet1(ex);
		WasmBool(__, __): makeSet1(ex);
		WasmRequire(__, __, __, __): makeSet1(ex); 
		WasmUnsafe(__, fallback, __, __): fiWasmFindLastStatements(fallback);
        WasmIncRefcnt(__, __, __): makeSet1(ex);
        WasmDecRefcnt(__, __, __): makeSet1(ex);
	}
}

fiWasmGenerateDestructors(vars : [Pair<string, WasmTypeDescriptor>]) {
    filtered = filter(vars, \v -> wasmTypeUsesWasmHeap(v.second));
    fold(filtered, "", \acc, v -> acc + " get_local $" + v.first + " call $wasm_dec_refcnt ;; auto-destruct \n");
}

fiWasmGenerateConstructor(type : WasmTypeDescriptor) {
	if (wasmTypeUsesWasmHeap(type)) Wasm2Call("$wasm_inc_refcnt_passthru")
	else Wasm2Void()
}

fiWasmTypeUsesHeap(t : FiType) {
	switch (t) {
		FiTypeVoid(): false;
		FiTypeDouble(): false;
		FiTypeInt(): false;
		FiTypeBool(): false;
		FiTypeNative(): false;
		default: true;
	}
}

fiWasmInjectTempVariables(ex : FiExp) {
	splitExpressions(ex, fiWasmTypeUsesHeap);
}

wrapAsIncRefcount(e: WasmExp) -> WasmExp {
    switch (e) {
        WasmVar(__, type, __): {
            if (wasmTypeUsesWasmHeap(type))
                WasmIncRefcnt(e, type, getNextWasmExpId())
            else
                e;
        }
        default: e;
    }
}

fiWasmInjectIncrements(ex : WasmExp) {
    switch (ex : WasmExp) {
		WasmLambda(args, b, type, __): ex;
		WasmCall(f, args, type, callType, id): 
            WasmCall(f, 
                map(args, fiWasmInjectIncrements), type, callType, id);
		WasmVar(name, type, __): 
            if (wasmTypeUsesWasmHeap(type))
                WasmIncRefcnt(ex, type, getNextWasmExpId())
            else
                ex;
		WasmLet(name, type, e1, e2, type2, id): 
			WasmLet(name, type, 
                fiWasmInjectIncrements(e1), 
                fiWasmInjectIncrements(e2), type2, id); 
		WasmIf(e1, e2, e3, type, id): 
			WasmIf(fiWasmInjectIncrements(e1), fiWasmInjectIncrements(e2), fiWasmInjectIncrements(e3), type, id);
		WasmSwitch(x, switchType, cases, type, id): 
			WasmSwitch(x, switchType, 
                map(cases, \c -> WasmCase(c.struct, c.argNames, fiWasmInjectIncrements(c.body), c.id)),
                type, id);
		WasmCast(e, tFrom, tTo, type, id): 
            WasmCast(fiWasmInjectIncrements(e), tFrom, tTo, type, id);
		WasmSeq(es, type, iid): 
            WasmSeq(map(es, fiWasmInjectIncrements), type, iid);
		WasmCallPrim(op, es, type, id): WasmCallPrim(op, map(es, fiWasmInjectIncrements), type, id);
		WasmVoid(__): ex;
		WasmDouble(__, __): ex;
		WasmInt(__, __): ex;
		WasmString(__, __): WasmIncRefcnt(ex, WasmTypeString(), getNextWasmExpId());
		WasmBool(__, __): ex;
		WasmRequire(flowfile, e, type, id): WasmRequire(flowfile, fiWasmInjectIncrements(e), type, id); 
		WasmUnsafe(name, fallback, type, id): WasmUnsafe(name, fiWasmInjectIncrements(fallback), type, id);
        WasmIncRefcnt(__, __, __): ex; // should not happen
        WasmDecRefcnt(__, __, __): ex;
	}
}
